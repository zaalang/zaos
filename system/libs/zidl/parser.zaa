//
// zidl parser
//

import std.stdio;
import std.string : String;
import zidl.lexer : source_text, source_location, token, lex_cursor;
import diag;

pub struct ast
{
  pub std::string file;
  pub std::string name;
  pub std::vector<decl> decls;

  pub ast() = default;
  pub ast(ast&&) = default;
  pub ~ast() = default;
}

pub struct constant
{
  pub std::string name;
  pub std::string value;

  pub constant() = default;
  pub constant(constant&&) = default;
  pub ~constant() = default;
}

pub struct protocol
{
  pub std::string name;
  pub std::vector<decl> methods;

  pub protocol() = default;
  pub protocol(protocol&&) = default;
  pub ~protocol() = default;
}

pub struct method
{
  pub enum type
  {
    oneway,
    twoway,
    event,
  }

  pub type type;
  pub i32 ordinal;
  pub std::string name;
  pub std::vector<decl> parameters;
  pub std::vector<decl> returns;

  pub method() = default;
  pub method(method&&) = default;
  pub ~method() = default;
}

pub struct variable
{
  pub std::string name;
  pub std::string type;
  pub usize constraint;

  pub variable() = default;
  pub variable(variable&&) = default;
  pub ~variable() = default;
}

pub struct bits
{
  pub std::string name;
  pub std::vector<decl> constants;

  pub bits() = default;
  pub bits(bits&&) = default;
  pub ~bits() = default;
}

pub struct enumerator
{
  pub std::string name;
  pub std::vector<decl> constants;

  pub enumerator() = default;
  pub enumerator(enumerator&&) = default;
  pub ~enumerator() = default;
}

pub struct structure
{
  pub std::string name;
  pub std::vector<decl> fields;

  pub structure() = default;
  pub structure(structure&&) = default;
  pub ~structure() = default;
}

pub union decl
{
  pub constant(constant),
  pub variable(variable),
  pub bits(bits),
  pub enumerator(enumerator),
  pub structure(structure),
  pub protocol(protocol),
  pub method(method),

  pub fn bool(this &) -> bool
  {
    return this.kind != cast(0);
  }

  pub decl() = default;
  pub decl(decl&&) = default;
  pub ~decl() = default;
}

union ParseError
{
  struct ExpectedLibrary : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0001]";
      super.message = "Expected Library";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, tok.text.len, "expected library");
    }

    ~this() = default;
  }

  struct ExpectedDeclaration : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0002]";
      super.message = "Expected Declaration";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, tok.text.len, "expected a declaration");
    }

    ~this() = default;
  }

  struct ExpectedIdentifier : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0003]";
      super.message = "Expected Identifier";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, tok.text.len, "expected an identifier");
    }

    ~this() = default;
  }

  struct ExpectedAssignment : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0004]";
      super.message = "Expected Assignment";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected '='");
    }

    ~this() = default;
  }

  struct ExpectedArraySize : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0005]";
      super.message = "Expected Array Size";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected a number");
    }

    ~this() = default;
  }

  struct ExpectedOrdinal : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0006]";
      super.message = "Expected Ordinal";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected a number");
    }

    ~this() = default;
  }

  struct ExpectedConstraint : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0007]";
      super.message = "Expected Constraint";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected a number");
    }

    ~this() = default;
  }

  struct ExpectedParameterList : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0008]";
      super.message = "Expected Parameter List";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected a parameter list");
    }

    ~this() = default;
  }

  struct ExpectedReturnsList : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0009]";
      super.message = "Expected Returns List";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected a returns list");
    }

    ~this() = default;
  }

  struct ExpectedClosingBracket : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0010]";
      super.message = "Expected Closing Bracket";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected ']'");
    }

    ~this() = default;
  }

  struct ExpectedClosingParen : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0011]";
      super.message = "Expected Closing Paren";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected ')'");
    }

    ~this() = default;
  }

  struct ExpectedOpeningBrace : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0012]";
      super.message = "Expected Opening Brace";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected '{'");
    }

    ~this() = default;
  }

  struct ExpectedClosingBrace : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0013]";
      super.message = "Expected Closing Brace";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected '}'");
    }

    ~this() = default;
  }

  struct ExpectedSemi : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[E0014]";
      super.message = "Expected Semi";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "expected ';'");
    }

    ~this() = default;
  }

  struct ExtraSemi : pub diag::diagnostic
  {
    this(source_text &text, token &tok)
    {
      super.code = "[W0015]";
      super.message = "Extra Semi";
      super.loc = std::format("{}:{}:{}", text.path, tok.loc.lineno, tok.loc.charpos);
      super.source = diag::source_file(text.path);
      super.add_label(tok.text.data - text.head, 1, "extra ';'");
    }

    ~this() = default;
  }
}

struct parse_context
{
  token tok;
  lex_cursor lexcursor;

  diag::diag mut *diag;
  source_text text;

  fn consume_token(this mut &ctx) -> void
  {
    ctx.lexcursor = lex(ctx.text, ctx.lexcursor, &mut ctx.tok);
  }

  fn consume_token(this mut &ctx, token::type expected) -> void
  {
    std::assert(ctx.tok == expected);

    consume_token(&mut ctx);
  }

  fn try_consume_token(this mut &ctx, token::type expected) -> bool
  {
    if (ctx.tok == expected)
    {
      consume_token(&mut ctx);

      return true;
    }

    return false;
  }

  fn consume_til(this mut &ctx, token::type type) -> void
  {
    while (ctx.tok != type && ctx.tok != token::type::eof)
    {
      consume_token(&mut ctx);
    }
  }

  fn consume_til_resumable(this mut &ctx) -> void
  {
    while (true)
    {
      switch (ctx.tok.type)
      {
        case l_brace:
          consume_til(&mut ctx, token::type::r_brace);
          consume_token(&mut ctx);
          return;

        case l_square:
          consume_til(&mut ctx, token::type::r_square);
          consume_token(&mut ctx);

        case l_paren:
          consume_til(&mut ctx, token::type::r_paren);
          consume_token(&mut ctx);

        case semi:
          consume_token(&mut ctx);
          return;

        case r_brace:
          consume_token(&mut ctx);
          return;

        case eof:
          return;

        else:
          consume_token(&mut ctx);
      }
    }
  }

  parse_context() = default;
  ~parse_context() = default;
}

fn parse_qualified_name(parse_context mut &ctx) -> std::string
{
  var loc = ctx.tok.loc;
  var name = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::identifier))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  while (ctx.try_consume_token(token::type::period))
  {
    name = std::string_view(name.data, name.len + ctx.tok.text.len + 1);

    if (!ctx.try_consume_token(token::type::identifier))
      ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));
  }

  return name;
}

fn parse_const_declaration(parse_context mut &ctx) -> decl
{
  var constant = constant();

  ctx.consume_token(token::type::kw_const);

  constant.name = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::identifier))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  if (!ctx.try_consume_token(token::type::equal))
    ctx.diag.error(ParseError::ExpectedAssignment(ctx.text, ctx.tok));

  constant.value = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::numeric_constant))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  if (!ctx.try_consume_token(token::type::semi))
    ctx.diag.error(ParseError::ExpectedSemi(ctx.text, ctx.tok));

  return decl::constant(&move constant);
}

fn parse_variable_declaration(parse_context mut &ctx) -> decl
{
  var variable = variable();

  variable.type = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::identifier))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  if (ctx.try_consume_token(token::type::l_angle))
  {
    variable.type += '<';
    variable.type += ctx.tok.text;
    variable.type += '>';

    if (!ctx.try_consume_token(token::type::identifier))
      ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

    if (!ctx.try_consume_token(token::type::r_angle))
      ctx.diag.error(ParseError::ExpectedClosingBracket(ctx.text, ctx.tok));
  }

  if (ctx.try_consume_token(token::type::l_square))
  {
    variable.type += '[';
    variable.type += ctx.tok.text;
    variable.type += ']';

    if (!ctx.try_consume_token(token::type::numeric_constant))
      ctx.diag.error(ParseError::ExpectedArraySize(ctx.text, ctx.tok));

    if (!ctx.try_consume_token(token::type::r_square))
      ctx.diag.error(ParseError::ExpectedClosingBracket(ctx.text, ctx.tok));
  }

  if (ctx.try_consume_token(token::type::colon))
  {
    std::sscan(ctx.tok.text, &mut variable.constraint);

    if (!ctx.try_consume_token(token::type::numeric_constant))
      ctx.diag.error(ParseError::ExpectedConstraint(ctx.text, ctx.tok));
  }

  variable.name = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::identifier))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  return decl::variable(&move variable);
}

fn parse_method_declaration(parse_context mut &ctx) -> decl
{
  var method = method();

  switch (ctx.tok.type)
  {
    case identifier:

      method.type = method::type::oneway;

      method.name = ctx.tok.text;
      method.ordinal = cast<i32>(std::hash()(method.name) >> 16 & 0x7ffffff);

      ctx.consume_token(token::type::identifier);

      if (!ctx.try_consume_token(token::type::l_paren))
        ctx.diag.error(ParseError::ExpectedParameterList(ctx.text, ctx.tok));

      while (ctx.tok != token::type::r_paren && ctx.tok != token::type::eof)
      {
        if (var decl = parse_variable_declaration(&mut ctx); decl)
        {
          method.parameters.push_back(&move decl);
        }

        if (!ctx.try_consume_token(token::type::comma))
          break;
      }

      if (!ctx.try_consume_token(token::type::r_paren))
        ctx.diag.error(ParseError::ExpectedClosingParen(ctx.text, ctx.tok));

      if (ctx.try_consume_token(token::type::arrow))
      {
        method.type = method::type::twoway;

        if (!ctx.try_consume_token(token::type::l_paren))
          ctx.diag.error(ParseError::ExpectedReturnsList(ctx.text, ctx.tok));

        while (ctx.tok != token::type::r_paren && ctx.tok != token::type::eof)
        {
          if (var decl = parse_variable_declaration(&mut ctx); decl)
          {
            method.returns.push_back(&move decl);
          }

          if (!ctx.try_consume_token(token::type::comma))
            break;
        }

        if (!ctx.try_consume_token(token::type::r_paren))
          ctx.diag.error(ParseError::ExpectedClosingParen(ctx.text, ctx.tok));
      }

    case arrow:

      method.type = method::type::event;

      ctx.consume_token(token::type::arrow);

      method.name = ctx.tok.text;
      method.ordinal = cast<i32>(std::hash()(method.name) & 0x7ffffff);

      if (!ctx.try_consume_token(token::type::identifier))
        ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

      if (!ctx.try_consume_token(token::type::l_paren))
        ctx.diag.error(ParseError::ExpectedReturnsList(ctx.text, ctx.tok));

      while (ctx.tok != token::type::r_paren && ctx.tok != token::type::eof)
      {
        if (var decl = parse_variable_declaration(&mut ctx); decl)
        {
          method.returns.push_back(&move decl);
        }

        if (!ctx.try_consume_token(token::type::comma))
          break;
      }

      if (!ctx.try_consume_token(token::type::r_paren))
        ctx.diag.error(ParseError::ExpectedClosingParen(ctx.text, ctx.tok));

    else:
      ctx.diag.error(ParseError::ExpectedDeclaration(ctx.text, ctx.tok));
      ctx.consume_til_resumable();
  }

  if (ctx.try_consume_token(token::type::equal))
  {
    std::sscan(ctx.tok.text, &mut method.ordinal);

    if (!ctx.try_consume_token(token::type::numeric_constant))
      ctx.diag.error(ParseError::ExpectedOrdinal(ctx.text, ctx.tok));
  }

  if (!ctx.try_consume_token(token::type::semi))
    ctx.diag.error(ParseError::ExpectedSemi(ctx.text, ctx.tok));

  return decl::method(method);
}

fn parse_protocol_declaration(parse_context mut &ctx) -> decl
{
  var protocol = protocol();

  ctx.consume_token(token::type::kw_protocol);

  protocol.name = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::identifier))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  if (!ctx.try_consume_token(token::type::l_brace))
    ctx.diag.error(ParseError::ExpectedOpeningBrace(ctx.text, ctx.tok));

  while (ctx.tok != token::type::r_brace && ctx.tok != token::type::eof)
  {
    if (var decl = parse_method_declaration(&mut ctx); decl)
    {
      protocol.methods.push_back(&move decl);
    }
  }

  if (!ctx.try_consume_token(token::type::r_brace))
    ctx.diag.error(ParseError::ExpectedClosingBrace(ctx.text, ctx.tok));

  return decl::protocol(&move protocol);
}

fn parse_bits_declaration(parse_context mut &ctx) -> decl
{
  var bits = bits();

  ctx.consume_token(token::type::kw_bits);

  bits.name = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::identifier))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  if (!ctx.try_consume_token(token::type::l_brace))
    ctx.diag.error(ParseError::ExpectedOpeningBrace(ctx.text, ctx.tok));

  while (ctx.tok != token::type::r_brace && ctx.tok != token::type::eof)
  {
    var constant = constant();

    constant.name = ctx.tok.text;

    if (!ctx.try_consume_token(token::type::identifier))
      ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

    if (!ctx.try_consume_token(token::type::equal))
      ctx.diag.error(ParseError::ExpectedAssignment(ctx.text, ctx.tok));

    constant.value = ctx.tok.text;

    if (!ctx.try_consume_token(token::type::numeric_constant))
      ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

    bits.constants.push_back(&move decl::constant(&move constant));

    if (!ctx.try_consume_token(token::type::comma))
      break;
  }

  if (!ctx.try_consume_token(token::type::r_brace))
    ctx.diag.error(ParseError::ExpectedClosingBrace(ctx.text, ctx.tok));

  return decl::bits(&move bits);
}

fn parse_enum_declaration(parse_context mut &ctx) -> decl
{
  var enumerator = enumerator();

  ctx.consume_token(token::type::kw_enum);

  enumerator.name = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::identifier))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  if (!ctx.try_consume_token(token::type::l_brace))
    ctx.diag.error(ParseError::ExpectedOpeningBrace(ctx.text, ctx.tok));

  while (ctx.tok != token::type::r_brace && ctx.tok != token::type::eof)
  {
    var constant = constant();

    constant.name = ctx.tok.text;

    if (!ctx.try_consume_token(token::type::identifier))
      ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

    if (!ctx.try_consume_token(token::type::equal))
      ctx.diag.error(ParseError::ExpectedAssignment(ctx.text, ctx.tok));

    constant.value = ctx.tok.text;

    if (!ctx.try_consume_token(token::type::numeric_constant))
      ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

    enumerator.constants.push_back(&move decl::constant(&move constant));

    if (!ctx.try_consume_token(token::type::comma))
      break;
  }

  if (!ctx.try_consume_token(token::type::r_brace))
    ctx.diag.error(ParseError::ExpectedClosingBrace(ctx.text, ctx.tok));

  return decl::enumerator(&move enumerator);
}

fn parse_struct_declaration(parse_context mut &ctx) -> decl
{
  var structure = structure();

  ctx.consume_token(token::type::kw_struct);

  structure.name = ctx.tok.text;

  if (!ctx.try_consume_token(token::type::identifier))
    ctx.diag.error(ParseError::ExpectedIdentifier(ctx.text, ctx.tok));

  if (!ctx.try_consume_token(token::type::l_brace))
    ctx.diag.error(ParseError::ExpectedOpeningBrace(ctx.text, ctx.tok));

  while (ctx.tok != token::type::r_brace && ctx.tok != token::type::eof)
  {
    if (var decl = parse_variable_declaration(&mut ctx); decl)
    {
      structure.fields.push_back(&move decl);
    }

    if (!ctx.try_consume_token(token::type::semi))
      break;
  }

  if (!ctx.try_consume_token(token::type::r_brace))
    ctx.diag.error(ParseError::ExpectedClosingBrace(ctx.text, ctx.tok));

  return decl::structure(&move structure);
}

fn parse_toplevel_declaration(parse_context mut &ctx) -> decl
{
  var loc = ctx.tok.loc;

  switch (ctx.tok.type)
  {
    case semi:
      ctx.diag.warn(ParseError::ExtraSemi(ctx.text, ctx.tok));
      ctx.consume_token(token::type::semi);

    case kw_const:
      return parse_const_declaration(&mut ctx);

    case kw_protocol:
      return parse_protocol_declaration(&mut ctx);

    case kw_bits:
      return parse_bits_declaration(&mut ctx);

    case kw_enum:
      return parse_enum_declaration(&mut ctx);

    case kw_struct:
      return parse_struct_declaration(&mut ctx);

    else:
      ctx.diag.error(ParseError::ExpectedDeclaration(ctx.text, ctx.tok));
      ctx.consume_til_resumable();
  }

  return decl();
}

pub fn parse(String &path, diag::diag mut &diag) throws(std::error) -> ast
{
  var ast = ast();

  var ctx = parse_context();
  ctx.diag = &diag;
  ctx.text = source_text::open(path);
  ctx.consume_token();

  ast.file = path;

  if (!ctx.try_consume_token(token::type::kw_library))
    ctx.diag.error(ParseError::ExpectedLibrary(ctx.text, ctx.tok));

  ast.name = parse_qualified_name(&mut ctx);

  if (!ctx.try_consume_token(token::type::semi))
    ctx.diag.error(ParseError::ExpectedSemi(ctx.text, ctx.tok));

  while (ctx.tok != token::type::eof)
  {
    if (var decl = parse_toplevel_declaration(&mut ctx); decl)
    {
      ast.decls.push_back(&move decl);
    }
  }

  return ast;
}
