//
// gfx stroke
//

import std.math;
import gfx.path;
import gfx.color;
import gfx.blend;
import gfx.point;
import gfx.transform;
import gfx.raster;
import lml.fixed;

struct state
{
  gfx::point start;
  gfx::point cursor;
  gfx::raster_t raster;

  state() = default;
  ~state() = default;
}

fn stroke_line(gfx::raster_t &&raster, gfx::transform &Q, gfx::point &v1, gfx::point &v2, float width) -> void
{
  using lml;
  using std;

  var halfwidth = 0.5 * width;

  var oab = normalise(perp(vec(v2, v1)));

  var a1 = Q * (v1 - halfwidth * oab);
  var a2 = Q * (v1 + halfwidth * oab);

  var b1 = Q * (v2 - halfwidth * oab);
  var b2 = Q * (v2 + halfwidth * oab);

  raster.move_to(a1.x, a1.y);
  raster.line_to(b1.x, b1.y);
  raster.line_to(b2.x, b2.y);
  raster.line_to(a2.x, a2.y);
  raster.line_to(a1.x, a1.y);
}

fn stroke_quad(gfx::raster_t &&raster, gfx::transform &Q, gfx::point &v1, gfx::point &v2, gfx::point &v3, float width) -> void
{
  using lml;
  using std;

  fn subdivide[raster, Q, width](gfx::point &v1, gfx::point &v2, gfx::point &v3, int n) -> void
  {
    var d = abs(v1.x - v2.x) + abs(v1.y - v2.y) + abs(v3.x - v2.x) + abs(v3.y - v2.y);

    if (v1.x == v2.x && v1.y == v2.y)
    {
      stroke_line(raster, Q, v2, v3, width);

      return;
    }

    if (v2.x == v3.x && v2.y == v3.y)
    {
      stroke_line(raster, Q, v1, v2, width);

      return;
    }

    if (d <= abs(v3.x - v1.x) + abs(v3.y - v1.y) || n <= 0)
    {
      var halfwidth = 0.5 * width;

      var oab = normalise(perp(vec(v2, v1)));
      var oac = normalise(perp(vec(v3, v1)));
      var obc = normalise(perp(vec(v3, v2)));

      var a1 = Q * (v1 - halfwidth * oab);
      var a2 = Q * (v1 + halfwidth * oab);

      var b1 = Q * (v2 - halfwidth * oac);
      var b2 = Q * (v2 + halfwidth * oac);

      var c1 = Q * (v3 - halfwidth * obc);
      var c2 = Q * (v3 + halfwidth * obc);

      raster.move_to(a1.x, a1.y);
      raster.quad_to(b1.x, b1.y, c1.x, c1.y);
      raster.line_to(c2.x, c2.y);
      raster.quad_to(b2.x, b2.y, a2.x, a2.y);
      raster.line_to(a1.x, a1.y);

      return;
    }

    var p1 = 0.5 * v1 + 0.5 * v2;
    var p2 = 0.25 * v1 + 0.5 * v2 + 0.25 * v3;
    var p3 = 0.5 * v2 + 0.5 * v3;

    subdivide(v1, p1, p2, n - 1);
    subdivide(p2, p3, v3, n - 1);
  }

  subdivide(v1, v2, v3, 2);
}

fn stroke_cubic(gfx::raster_t &&raster, gfx::transform &Q, gfx::point &v1, gfx::point &v2, gfx::point &v3, gfx::point &v4, float width) -> void
{
  using lml;
  using std;

  fn subdivide[raster, Q, width](gfx::point &v1, gfx::point &v2, gfx::point &v3, gfx::point &v4, int n) -> void
  {
    var d = abs(v1.x + v3.x - v2.x - v2.x) + abs(v1.y + v3.y - v2.y - v2.y) + abs(v2.x + v4.x - v3.x - v3.x) + abs(v2.y + v4.y - v3.y - v3.y);

    if (v1.x == v2.x && v1.y == v2.y)
    {
      stroke_quad(raster, Q, v2, v3, v4, width);

      return;
    }

    if (v3.x == v4.x && v3.y == v4.y)
    {
      stroke_quad(raster, Q, v1, v2, v3, width);

      return;
    }

    if (d <= abs(v3.x - v1.x) + abs(v3.y - v1.y) || n <= 0)
    {
      var halfwidth = 0.5 * width;

      var oab = normalise(perp(vec(v2, v1)));
      var oac = normalise(perp(vec(v3, v1)));
      var obd = normalise(perp(vec(v4, v2)));
      var ocd = normalise(perp(vec(v4, v3)));

      var a1 = Q * (v1 - halfwidth * oab);
      var a2 = Q * (v1 + halfwidth * oab);

      var b1 = Q * (v2 - halfwidth * oac);
      var b2 = Q * (v2 + halfwidth * oac);

      var c1 = Q * (v3 - halfwidth * obd);
      var c2 = Q * (v3 + halfwidth * obd);

      var d1 = Q * (v4 - halfwidth * ocd);
      var d2 = Q * (v4 + halfwidth * ocd);

      raster.move_to(a1.x, a1.y);
      raster.cubic_to(b1.x, b1.y, c1.x, c1.y, d1.x, d1.y);
      raster.line_to(d2.x, d2.y);
      raster.cubic_to(c2.x, c2.y, b2.x, b2.y, a2.x, a2.y);
      raster.line_to(a1.x, a1.y);

      return;
    }

    var p1 = 0.5 * v1 + 0.5 * v2;
    var p2 = 0.25 * v1 + 0.5 * v2 + 0.25 * v3;
    var p3 = 0.125 * v1 + 0.375 * v2 + 0.375 * v3 + 0.125 * v4;
    var p4 = 0.25 * v2 + 0.5 * v3 + 0.25 * v4;
    var p5 = 0.5 * v3 + 0.5 * v4;

    subdivide(v1, p1, p2, p3, n - 1);
    subdivide(p3, p4, p5, v4, n - 1);
  }

  subdivide(v1, v2, v3, v4, 3);
}

fn stroke_conic(gfx::raster_t &&raster, gfx::transform &Q, gfx::point &v1, gfx::point &v2, gfx::point &v3, float w, float width) -> void
{
  using lml;
  using std;

  fn subdivide[raster, Q, width](gfx::point &v1, gfx::point &v2, gfx::point &v3, float w, int n) -> void
  {
    if (v1.x == v2.x && v1.y == v2.y)
    {
      stroke_line(raster, Q, v2, v3, width);

      return;
    }

    if (v2.x == v3.x && v2.y == v3.y)
    {
      stroke_line(raster, Q, v1, v2, width);

      return;
    }

    if (n <= 0)
    {
      var halfwidth = 0.5 * width;

      var oab = normalise(perp(vec(v2, v1)));
      var oac = normalise(perp(vec(v3, v1)));
      var obc = normalise(perp(vec(v3, v2)));

      var a1 = Q * (v1 - halfwidth * oab);
      var a2 = Q * (v1 + halfwidth * oab);

      var b1 = Q * (v2 - halfwidth * oac);
      var b2 = Q * (v2 + halfwidth * oac);

      var c1 = Q * (v3 - halfwidth * obc);
      var c2 = Q * (v3 + halfwidth * obc);

      raster.move_to(a1.x, a1.y);
      raster.conic_to(b1.x, b1.y, c1.x, c1.y, w);
      raster.line_to(c2.x, c2.y);
      raster.conic_to(b2.x, b2.y, a2.x, a2.y, w);
      raster.line_to(a1.x, a1.y);

      return;
    }

    var u = 1.0 / (1.0 + w);
    var wu = w * u;

    var p1 = u * v1 + wu * v2;
    var p2 = 0.5 * u * v1 + wu * v2 + 0.5 * u * v3;
    var p3 = wu * v2 + u * v3;
    var pw = sqrt(0.5 + 0.5 * w);

    subdivide(v1, p1, p2, pw, n - 1);
    subdivide(p2, p3, v3, pw, n - 1);
  }

  subdivide(v1, v2, v3, w, 2);
}

fn move_to(state &&state, gfx::transform &Q, float x, float y) -> void
{
  state.cursor = state.start = gfx::point(x, y);
}

fn line_to(state &&state, gfx::transform &Q, float x, float y, float thickness) -> void
{
  stroke_line(state.raster, Q, state.cursor, gfx::point(x, y), thickness);

  state.cursor = gfx::point(x, y);
}

fn quad_to(state &&state, gfx::transform &Q, float x1, float y1, float x, float y, float thickness) -> void
{
  stroke_quad(state.raster, Q, state.cursor, gfx::point(x1, y1), gfx::point(x, y), thickness);

  state.cursor = gfx::point(x, y);
}

fn cubic_to(state &&state, gfx::transform &Q, float x1, float y1, float x2, float y2, float x, float y, float thickness) -> void
{
  stroke_cubic(state.raster, Q, state.cursor, gfx::point(x1, y1), gfx::point(x2, y2), gfx::point(x, y), thickness);

  state.cursor = gfx::point(x, y);
}

fn conic_to(state &&state, gfx::transform &Q, float x1, float y1, float x, float y, float w, float thickness) -> void
{
  stroke_conic(state.raster, Q, state.cursor, gfx::point(x1, y1), gfx::point(x, y), w, thickness);

  state.cursor = gfx::point(x, y);
}

fn close(state &&state, gfx::transform &Q, float thickness) -> void
{
  if (state.cursor != state.start)
    stroke_line(state.raster, Q, state.cursor, state.start, thickness);

  state.cursor = state.start;
}

pub fn stroke_path(gfx::paint_t &&ctx, gfx::rect &clip, gfx::transform &Q, gfx::path &path, float thickness, gfx::color &color, gfx::blend mode = gfx::blend::source_over) -> void
{
  var state = state();

  state.raster.clip(std::clamp(clip.left, 0.0, cast(ctx.width)), std::clamp(clip.top, 0.0, cast(ctx.height)), std::clamp(clip.right, 0.0, cast(ctx.width)), std::clamp(clip.bottom, 0.0, cast(ctx.height)));

  for (var &vertex : path.vertices)
  {
    switch (vertex)
    {
      case move_to[pt]:
        move_to(state, Q, pt.x, pt.y);

      case line_to[pt]:
        line_to(state, Q, pt.x, pt.y, thickness);

      case quad_to[pt]:
        quad_to(state, Q, pt.x1, pt.y1, pt.x, pt.y, thickness);

      case cubic_to[pt]:
        cubic_to(state, Q, pt.x1, pt.y1, pt.x2, pt.y2, pt.x, pt.y, thickness);

      case conic_to[pt]:
        conic_to(state, Q, pt.x1, pt.y1, pt.x, pt.y, pt.w, thickness);

      case close:
        close(state, Q, thickness);
    }
  }

  var color = gfx::premultiply(color.rgba);

  state.raster.accumulate(|scanline| {

    var row = cast<u32 mut *>(ctx.data + cast<usize>(scanline.y) * ctx.stride);

    for (var span : scanline)
    {
      var alpha = lml::clamp(span.alpha * 255, 0, 255).as<u32>();

      if (alpha <= 0)
        continue;

      for (var x = cast<usize>(span.x), var end = cast<usize>(span.x) + span.len; x != end; ++x)
      {
        *(row + x) = gfx::blend(mode, *(row + x), gfx::premultiply(color, alpha));
      }
    }
  });
}

pub fn stroke_path(gfx::paint_t &&ctx, gfx::transform &Q, gfx::path &path, float thickness, gfx::color &color, gfx::blend mode = gfx::blend::source_over) -> void
{
  stroke_path(ctx, gfx::rect(0.0, 0.0, cast(ctx.width), cast(ctx.height)), Q, path, thickness, color, mode);
}
