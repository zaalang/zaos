//
// start
//

import std.atomic;
import elf;
import elf.auxvec;
import os.zaos.mman;
import os.zaos.fcntl;
import os.zaos.sched;
import os.zaos.unistd;

struct arg
{
  u8 *beg;
  u8 *end;
}

struct tls_module
{
  uintptr base;
  uintptr vaddr;
  usize len;
  usize size;
  usize align;
}

struct thread_data
{
  thread_data mut *self;
  uintptr[2] mut *dtv;
  uintptr[3] pad;
  uintptr canary;

  i32 tid;
  i32 rootfd;
  os::ioring ring;

  fn (*start_routine)(void mut *) -> i32;
  void mut *start_argument;

  u64 flags;
  void *memory;
  usize size;

  u8[64] bytes;
}

const THREAD_DEAD = 0x01;
const THREAD_DETACHED = 0x02;

extern fn exit(i32 result) -> void;
extern fn main(int argc, arg *argv, arg *envp) -> i32;
extern fn memset(void mut *dst, int c, usize n) -> void mut *;
extern fn memcpy(void mut *dst, void *src, usize n) -> void mut *;

extern fn tls_module() -> tls_module mut *
{
  static tls = [<u8>: 0; sizeof<tls_module>];

  return cast<tls_module mut *>(&tls);
}

extern fn thread_self() -> thread_data mut *
{
  return cast<thread_data mut *>(__asm("mov $0, fs:0", "=r"));
}

extern fn thread_id() -> i32
{
  return thread_self.tid;
}

extern fn thread_ring() -> os::ioring mut &
{
  return &thread_self.ring;
}

extern fn tls_area(arg *envp) -> usize
{
  var envc = 0;
  while ((envp + envc).beg)
    ++envc;

  var auxv = cast<elf::auxval*>(envp + envc + 1);

  var phdr = 0;
  var phnum = 0;
  var phent = 0;

  for (var entry = auxv; entry.type != cast(0); ++entry)
  {
    switch (entry.type)
    {
      case PHDR:
        phdr = entry.value;

      case PHENT:
        phent = cast<usize>(entry.value);

      case PHNUM:
        phnum = cast<usize>(entry.value);
    }
  }

  var tls = tls_module();

  for (var i = 0; i < phnum; ++i)
  {
    var entry = cast<elf::elf64_phdr*>(phdr + i*phent);

    switch (entry.p_type)
    {
      case elf::elf64_ptype::PHDR:
        tls.base = phdr - cast(entry.p_vaddr);

      case elf::elf64_ptype::TLS:
        tls.vaddr = cast(entry.p_vaddr);
        tls.len = cast(entry.p_filesz);
        tls.size = cast(entry.p_memsz);
        tls.align = cast(entry.p_align);
    }
  }

  if (tls.align < 1)
    tls.align = 1;

  tls.size = (tls.size + tls.align - 1) & -tls.align;

  return 2*sizeof<void*> + sizeof<thread_data> + tls.size + tls.align;
}

extern fn init_tcb(void mut *memory, arg *envp) -> void
{
  var tls = tls_module();
  var tlsbase = (cast<uintptr>(memory) + tls.align - 1) & -tls.align;
  var dtv = cast<uintptr[2] mut *>(tlsbase + tls.size + sizeof<thread_data>);
  var td = cast<thread_data mut *>(tlsbase + tls.size);

  dtv[0] = 1;
  dtv[1] = tlsbase;
  memset(cast<void mut *>(tlsbase), 0, tls.size);
  memcpy(cast<void mut *>(tlsbase), cast<void*>(tls.base + tls.vaddr), tls.len);

  td.self = td;
  td.canary = 0xdeadbeef;
  td.dtv = dtv;

  var envc = 0;
  while ((envp + envc).beg)
    ++envc;

  var auxv = cast<elf::auxval*>(envp + envc + 1);

  for (var entry = auxv; entry.type != cast(0); ++entry)
  {
    switch (entry.type)
    {
      case PID:
        td.tid = cast<i32>(entry.value);

      case ROOTFD:
        td.rootfd = cast<i32>(entry.value);

      case RINGFD:
        td.ring.fd = cast<i32>(entry.value);

      case RINGHDR:
        td.ring.header = cast<os::ioring_header mut *>(entry.value);
    }
  }

  __asm("wrfsbase $0", "r", td);
}

struct pthread_attr
{

}

using pthread_t = void*;
using pthread_attr_t = pthread_attr*;

extern fn __thread_start(void **argv) -> void
{
  var td = cast<thread_data mut *>(*argv);

  var auxv = cast<elf::auxval*>(argv + 1);

  for (var entry = auxv; entry.type != cast(0); ++entry)
  {
    switch (entry.type)
    {
      case ROOTFD:
        td.rootfd = cast<i32>(entry.value);

      case RINGFD:
        td.ring.fd = cast<i32>(entry.value);

      case RINGHDR:
        td.ring.header = cast<os::ioring_header mut *>(entry.value);
    }
  }

  __asm("wrfsbase $0", "r", td);

  var rval = td.start_routine(td.start_argument);

  if (std::atomic_or(&td.flags, THREAD_DEAD) & THREAD_DETACHED != 0)
  {
    os::ioring_destroy(td.ring.fd);
    os::thread_munmap_exit(td.memory, td.size);
  }

  os::thread_exit(rval);
}

extern fn pthread_create(pthread_t mut *thread, pthread_attr_t *attr, fn (*start_routine)(void mut *) -> i32, void mut *start_argument) -> i32
{
  var size = 8388608;

  var mmvec = os::mmvec();
  mmvec.length = size;
  mmvec.flags = os::mmap::anonymous;
  mmvec.prot = os::prot::readwrite;

  var memory = null<u8 mut *>();
  if (var rc = os::mmap(-1, &mmvec, 1, &memory, 0); rc < 0)
    return rc;

  var self = thread_self();

  var tls = tls_module();
  var tlsbase = (cast<uintptr>(memory) + size - 2*sizeof<void*> - tls.size - sizeof<thread_data>) & -tls.align;
  var dtv = cast<uintptr[2] mut *>(tlsbase + tls.size + sizeof<thread_data>);
  var td = cast<thread_data mut *>(tlsbase + tls.size);
  var stack = tlsbase & ~15;

  dtv[0] = 1;
  dtv[1] = tlsbase;
  memset(cast<void mut *>(tlsbase), 0, tls.size);
  memcpy(cast<void mut *>(tlsbase), cast<void*>(tls.base + tls.vaddr), tls.len);

  td.self = td;
  td.canary = 0xdeadbeef;
  td.dtv = dtv;
  td.start_routine = start_routine;
  td.start_argument = start_argument;
  td.memory = memory;
  td.size = size;
  td.flags = 0;

  *thread = cast(td);

  if (var rc = os::thread_create(cast(stack), cast(extern(_thread_start)), td, os::priority::normal, &td.tid, 0); rc < 0)
  {
    os::munmap(memory, size);

    return rc;
  }

  return 0;
}

extern fn pthread_join(pthread_t thread, i32 mut *rval) -> i32
{
  var td = cast<thread_data mut *>(thread);

  if (var rc = os::wait(td.tid, rval, 0); rc < 0)
    return rc;

  os::ioring_destroy(td.ring.fd);
  os::munmap(td.memory, td.size);

  return 0;
}

extern fn pthread_detach(pthread_t thread) -> i32
{
  var td = cast<thread_data mut *>(thread);

  if (var rc = os::wait(td.tid, null, os::WNOHANG); rc < 0)
    return rc;

  if (std::atomic_or(&td.flags, THREAD_DETACHED) & THREAD_DEAD != 0)
  {
    os::ioring_destroy(td.ring.fd);
    os::munmap(td.memory, td.size);
  }

  return 0;
}

extern fn __start(int argc, arg *argv, arg *envp) -> void
{
  init_tcb(__alloca(tls_area(envp)), envp);

  exit(main(argc, argv, envp));
}
