//
// winton
//

import std.stdio;
import std.function;
import app.loop;
import zidl.zaos_sys;
import seat;
import display;
import compositor;
import shell;

struct service
{
  app::loop loop;

  std::flat_hash_map<std::string, std::delegate<(zidl::string_view, zidl::channel) -> void>> on_connect;

  service() = default;
  ~service() = default;
}

fn initialise(service mut &service) -> i32
{
  if (var rc = app::initialise(&mut service.loop); rc < 0)
    std::panic("winton: unable to init loop - ", cast<std::errc>(-rc));

  service.loop.connect_handler = &cast<fn (app::loop mut &, zidl::string_view, zidl::channel) -> void>(connect_handler);

  return 0;
}

fn connect_handler(app::loop mut &loop, zidl::string_view uri, zidl::channel channel) -> void
{
  var service = cast<service mut *>(cast<uintptr>(&loop) - offsetof(service::loop));

  switch (service.on_connect.get(uri.strip_prefix("zaos.winton/").cut('/').0))
  {
    case Some[callback]:
      callback(uri, &move channel);
  }
}

fn run(service mut &service) -> void
{
  import os.zaos.sched;
  import os.zaos.unistd;

  var param = os::sched_param();
  param.sched_priority = 15;

  os::sched_setparam(os::gettid(), &param);

  service.loop.run();
}

fn main()
{
  var service = service();

  if (var rc = initialise(&mut service); rc < 0)
    std::panic("winton: unable to initialise - ", cast<std::errc>(-rc));

  var seat = seat::create();
  if (var rc = seat::initialise(&mut seat); rc < 0)
    std::print("winton: failed to initialise input - ", cast<std::errc>(-rc));

  var display = display::create();
  if (var rc = display::initialise(&mut display); rc < 0)
    std::print("winton: failed to initialise display - ", cast<std::errc>(-rc));

  var compositor = compositor::create(&mut seat, &mut display);
  if (var rc = compositor::initialise(&mut compositor); rc < 0)
    std::print("winton: failed to initialise compositor - ", cast<std::errc>(-rc));

  service.on_connect.insert("compositor", |uri, channel| {
    compositor.accept(uri, &move channel);
  });

  var shell = shell::create(&mut compositor);
  if (var rc = shell::initialise(&mut shell); rc < 0)
    std::print("winton: failed to initialise shell - ", cast<std::errc>(-rc));

  service.on_connect.insert("shell", |uri, channel| {
    shell.accept(uri, &move channel);
  });

  service.run();
}
