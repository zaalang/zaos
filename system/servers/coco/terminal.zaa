//
// terminal
//

import std.stdio;
import std.span : Span;
import std.string : String;
import chrono : duration;
import os.zaos.fcntl;
import display;
import seat;
import font;
import pty;
import vt;
import readline;

const palette = [
  0xff000000, // black
  0xffcd0000, // red
  0xff00cd00, // green
  0xffcdcd00, // yellow
  0xff0000ee, // blue
  0xffcd00cd, // magenta
  0xff00cdcd, // cyan
  0xffe5e5e5, // light grey
  0xff7f7f7f, // dark grey
  0xffff0000, // light red
  0xff00ff00, // light green
  0xffffff00, // light yellow
  0xff5c5cff, // light blue
  0xffff00ff, // light magenta
  0xff00ffff, // light cyan
  0xffffffff, // white
  0xff000000, // foreground
  0xff009000, // background
];

const cursor_palette = [
  0xff00cd00, // green
  0xffcdcd00, // yellow
  0xff0000cd, // blue
  0xffcd0000, // red
  0xffcdcdcd, // buff
  0xff00cdcd, // cyan
  0xffcd00cd, // magenta
  0xffcd6600, // orange
];

enum modifiers
{
  const shift = 0x1;
  const control = 0x2;
  const alt = 0x4;
}

pub struct termin
{
  terminal mut *terminal;

  pub fn read(this mut &, u8 mut *data, usize len) throws(std::error) -> usize
  {
    var rc = this.terminal.read(std::mutable_span(data, len));

    if (rc < 0)
      throw std::system_error(cast<std::errc>(-rc));

    return cast<usize>(rc);
  }

  pub termin(terminal mut *terminal)
    : terminal(terminal)
  {
  }

  pub ~termin() = default;
}

pub struct termout
{
  terminal mut *terminal;

  pub fn write(this mut &, u8 *data, usize len) throws(std::error) -> usize
  {
    if (len == 0)
      return 0;

    if (var rc = this.terminal.write(std::span(data, len)); rc < 0)
      throw std::system_error(cast<std::errc>(-rc));

    this.terminal.flush();

    return len;
  }

  pub termout(terminal mut *terminal)
    : terminal(terminal)
  {
  }

  pub ~termout() = default;
}

pub struct terminal
{
  seat::seat mut &seat;
  display::display mut &display;

  terminal(seat::seat mut &seat, display::display mut &display)
    : seat(&seat), display(&display), termin(&this), termout(&this)
  {
  }

  int top;
  int left;
  int bottom;
  int right;

  pty::pty pty;
  font::font font;
  vt::screen screen;

  u32 modifiers;
  usize cursor_color;

  readline::default_history history;

  struct cell
  {
    char ch;
    vt::attrs attrs;

    cell() = default;
    cell(cell&&) = default;
    ~cell() = default;
  }

  std::vector<cell> cells;

  pub termin termin;
  pub termout termout;

  pub fn x(this &) { return this.screen.cursor.0; }
  pub fn y(this &) { return this.screen.cursor.1; }

  pub fn rows(this &) { return this.screen.rows; }
  pub fn columns(this &) { return this.screen.columns; }

  pub fn pty(this &&) { return &this.pty; }
  pub fn display(this &&) { return &this.display; }

  pub ~terminal() = default;
}

extern fn thread_ring() -> os::ioring mut &;

pub fn create(seat::seat mut &seat, display::display mut &display) -> terminal
{
  return terminal(&mut seat, &mut display);
}

pub fn initialise(terminal mut &terminal) -> i32
{
  terminal.top = std::align_down(terminal.display.height / 16, 16);
  terminal.left = std::align_down(terminal.display.width / 16, 8);
  terminal.bottom = std::align_up(terminal.display.height - terminal.top, 16);
  terminal.right = std::align_up(terminal.display.width - terminal.left, 8);

  var rows = cast<usize>(terminal.bottom - terminal.top) / 16;
  var columns = cast<usize>(terminal.right - terminal.left) / 8;

  terminal.screen.set_palette(palette);

  if (var result = terminal.screen.resize(columns, rows); !result)
    return cast(result);

  if (var rc = font::initialise(&mut terminal.font); rc < 0)
    return rc;

  if (var rc = pty::open_pty(&mut terminal.pty); rc < 0)
    return rc;

  if (var rc = terminal.pty.resize(columns, rows); rc < 0)
    return rc;

  terminal.clear();

  return 0;
}

pub fn set_font(terminal mut &terminal, String &path) -> i32
{
  if (var rc = terminal.font.load(path); rc < 0)
    return rc;

  return 0;
}

pub fn clear(terminal mut &this) -> void
{
  this.display.fill(0, 0, this.display.width, this.top, 0xff222222);
  this.display.fill(0, this.bottom, this.display.width, this.display.height, 0xff222222);
  this.display.fill(0, this.top, this.left, this.display.height, 0xff222222);
  this.display.fill(this.right, this.top, this.display.width, this.display.height, 0xff222222);

  this.flush();
  this.screen.erase_screen();
  this.screen.move_to(0, 0);
  this.cells.clear();
}

pub fn print(terminal mut &this, String &str) -> void
{
  this.screen.write(str.data, str.len);
}

pub fn render(terminal mut &this) -> void
{
  var rows = this.screen.rows;
  var columns = this.screen.columns;

  this.cells.resize(rows * columns + 1);

  this.screen.draw(|i, j, ch, n, attrs| {
    var mut &cell = this.cells[j*columns + i];

    if (cell.ch != ch || cell.attrs != attrs)
    {
      var x = this.left + cast<int>(i) * 8;
      var y = this.top + cast<int>(j) * 16;

      var foreground = attrs.foreground_color;
      var background = attrs.background_color;

      if (attrs.flags & vt::attrs::inverse != 0)
        std::swap(&mut foreground, &mut background);

      this.display.blit(x, y, this.font.lookup(ch), foreground, background);

      cell.ch = ch;
      cell.attrs = attrs;

      for (var k = 1; k < n; ++k)
        this.cells[j*columns + i + k].ch = 0;
    }
  });
}

pub fn pump(terminal mut &terminal, chrono::nanoseconds timeout = chrono::nanoseconds(-1)) -> i32
{
  var mut &pty = terminal.pty;
  var mut &seat = terminal.seat;
  var mut &driver = seat.driver;

  var events = 0;

  var sp = os::timespec(void);
  if (var rc = os::clock_gettime(os::clock::monotonic, &sp); rc < 0)
    return rc;

  var buffer = [u8(0); 4096];

  while (true)
  {
    var bytes = pty.read_master(buffer.data, buffer.len);

    if (bytes > 0)
    {
      terminal.screen.write(buffer.data, cast(bytes));
    }

    driver.dispatch();

    while (driver.has_events)
    {
      switch (driver.next_event)
      {
        case keyboard_key[kv]:

          switch (kv.state)
          {
            case pressed:

              switch (kv.key)
              {
                case LeftShift:
                case RightShift:
                  terminal.modifiers |= modifiers::shift;

                case LeftControl:
                case RightControl:
                  terminal.modifiers |= modifiers::control;

                case LeftAlt:
                case RightAlt:
                  terminal.modifiers |= modifiers::alt;
              }

              if (terminal.modifiers == modifiers::control)
              {
                switch (kv.key)
                {
                  case Home:
                    pty.write_master("\x1b[1;5H".data, 6);

                  case Left:
                    pty.write_master("\x1b[1;5D".data, 6);

                  case Up:
                    pty.write_master("\x1b[1;5A".data, 6);

                  case Right:
                    pty.write_master("\x1b[1;5C".data, 6);

                  case Down:
                    pty.write_master("\x1b[1;5B".data, 6);

                  case End:
                    pty.write_master("\x1b[1;5F".data, 6);

                  case Insert:
                    pty.write_master("\x1b[2;5~".data, 6);

                  case Delete:
                    pty.write_master("\x1b[3;5~".data, 6);

                  case Backspace:
                    pty.write_master("\x08".data, 1);

                  else:
                    if (kv.sym != 0)
                    {
                      var buf = [ cast<u8>(kv.sym) ];

                      pty.write_master(buf.data, buf.len);
                    }
                }
              }

              if (terminal.modifiers == modifiers::alt)
              {
                switch (kv.key)
                {
                  case Enter:
                    pty.write_master("\x1b\x0d".data, 2);

                  else:
                    if (kv.sym != 0)
                    {
                      var buf = [ cast<u8>('\x1b'), cast<u8>(kv.sym) ];

                      pty.write_master(buf.data, buf.len);
                    }
                }
              }

              if (terminal.modifiers == modifiers::shift)
              {
                switch (kv.key)
                {
                  case Home:
                    pty.write_master("\x1b[1;2H".data, 6);

                  case Left:
                    pty.write_master("\x1b[1;2D".data, 6);

                  case Up:
                    pty.write_master("\x1b[1;2A".data, 6);

                  case Right:
                    pty.write_master("\x1b[1;2C".data, 6);

                  case Down:
                    pty.write_master("\x1b[1;2B".data, 6);

                  case PageUp:
                    pty.write_master("\x1b[5;2~".data, 6);

                  case PageDown:
                    pty.write_master("\x1b[6;2~".data, 6);

                  case End:
                    pty.write_master("\x1b[1;2F".data, 6);

                  case Insert:
                    pty.write_master("\x1b[2;2~".data, 6);

                  case Delete:
                    pty.write_master("\x1b[3;2~".data, 6);

                  case Tab:
                    pty.write_master("\x1b[1;2Z".data, 6);

                  case Enter:
                  case NumEnter:
                    pty.write_master("\x1b[13;2u".data, 7);

                  else:
                    if (kv.sym != 0)
                    {
                      var buf = [0; 8];
                      var len = std::ctoa(buf.begin, buf.end, kv.sym).len;

                      pty.write_master(buf.data, len);
                    }
                }
              }

              if (terminal.modifiers == 0)
              {
                switch (kv.key)
                {
                  case Enter:
                    pty.write_master("\r".data, 1);

                  case Backspace:
                    pty.write_master("\x7f".data, 1);

                  case Left:
                    pty.write_master("\x1bOD".data, 3);

                  case Up:
                    pty.write_master("\x1bOA".data, 3);

                  case Right:
                    pty.write_master("\x1bOC".data, 3);

                  case Down:
                    pty.write_master("\x1bOB".data, 3);

                  case Insert:
                    pty.write_master("\x1b[2~".data, 4);

                  case Delete:
                    pty.write_master("\x1b[3~".data, 4);

                  case Home:
                    pty.write_master("\x1bOH".data, 3);

                  case End:
                    pty.write_master("\x1bOF".data, 3);

                  case PageUp:
                    pty.write_master("\x1b[5~".data, 4);

                  case PageDown:
                    pty.write_master("\x1b[6~".data, 4);

                  case F1:
                    pty.write_master("\x1bOP".data, 3);

                  case F2:
                    pty.write_master("\x1bOQ".data, 3);

                  case F3:
                    pty.write_master("\x1bOR".data, 3);

                  case F4:
                    pty.write_master("\x1bOS".data, 3);

                  case F5:
                    pty.write_master("\x1b[15~".data, 5);

                  case F6:
                    pty.write_master("\x1b[17~".data, 5);

                  case F7:
                    pty.write_master("\x1b[18~".data, 5);

                  case F8:
                    pty.write_master("\x1b[19~".data, 5);

                  case F9:
                    pty.write_master("\x1b[20~".data, 5);

                  case F10:
                    pty.write_master("\x1b[21~".data, 5);

                  case F11:
                    pty.write_master("\x1b[23~".data, 5);

                  case F12:
                    pty.write_master("\x1b[24~".data, 5);

                  else:
                    if (kv.sym != 0)
                    {
                      var buf = [0; 8];
                      var len = std::ctoa(buf.begin, buf.end, kv.sym).len;

                      pty.write_master(buf.data, len);
                    }
                }
              }

            case released:

              switch (kv.key)
              {
                case LeftShift:
                case RightShift:
                  terminal.modifiers &= ~modifiers::shift;

                case LeftControl:
                case RightControl:
                  terminal.modifiers &= ~modifiers::control;

                case LeftAlt:
                case RightAlt:
                  terminal.modifiers &= ~modifiers::alt;
              }
          }
      }

      events += 1;
    }

    if (bytes > 0)
      terminal.render();

    if (terminal.screen.cursor_visible)
    {
      var cx = terminal.left + cast<int>(terminal.screen.cursor.0) * 8;
      var cy = terminal.top + cast<int>(terminal.screen.cursor.1) * 16;

      if (terminal.left <= cx && cx < terminal.right && terminal.top <= cy && cy < terminal.bottom)
        terminal.display.fill(cx, cy, cx + 8, cy + 16, cursor_palette[terminal.cursor_color]);

      if (terminal.screen.cursor.1*terminal.screen.columns + terminal.screen.cursor.0 < terminal.cells.len)
        terminal.cells[terminal.screen.cursor.1*terminal.screen.columns + terminal.screen.cursor.0].ch = 0;

      terminal.cursor_color += 1;
      if (terminal.cursor_color == cursor_palette.len)
        terminal.cursor_color = 0;
    }

    if (events != 0)
      return events;

    var tp = os::timespec(void);
    if (var rc = os::clock_gettime(os::clock::monotonic, &tp); rc < 0)
      return rc;

    if (timeout.count >= 0)
    {
      if (tp - sp > cast(timeout.count))
        return 0;
    }

    var pollset = [
      os::pollevt(pty.master, os::POLLFD, os::POLLIN, 0),
      os::pollevt(driver.fd, os::POLLFD, os::POLLIN, 0),
      os::pollevt(seat.plugfd, os::POLLFD, os::POLLIN, 0),
    ];

    var cnt = os::select(&mut thread_ring, pollset.data, pollset.len, tp + 1_000_000_000 / 8);

    for (var i = 0; i < cnt; ++i)
    {
      if (pollset[cast<usize>(i)].id == seat.plugfd)
        seat::hotplug(&mut seat);
    }
  }
}

pub fn pump<R, P>(terminal mut &terminal, chrono::duration<R, P> timeout) -> i32
{
  return terminal.pump(timeout.nanoseconds);
}

pub fn write(terminal mut &terminal, Span<u8> &buffer) -> i32
{
  var remaining = buffer.len;

  while (remaining != 0)
  {
    var bytes = terminal.pty.write_slave(buffer.data + (buffer.len - remaining), remaining);

    if (bytes < 0)
    {
      if (bytes != os::EAGAIN)
        return bytes;

      terminal.pump(0.nanoseconds);

      bytes = 0;
    }

    remaining -= cast(bytes);
  }

  return cast(buffer.len);
}

pub fn read(terminal mut &terminal, Span<u8> mut &buffer) -> i32
{
  terminal.pump(0.nanoseconds);

  while (true)
  {
    var bytes = terminal.pty.read_slave(buffer.data, buffer.len);

    if (bytes != os::EAGAIN)
      return bytes;

    terminal.pump();
  }
}

pub fn read_pass(terminal mut &terminal, Span<u8> mut &buffer) -> i32
{
  var mut &pty = terminal.pty;

  pty.echo_off();

  var bytes = terminal.read(&mut buffer);

  pty.echo_on();

  return bytes;
}

pub fn read_line(terminal mut &terminal, readline::completer mut *completer = null) -> readline::result
{
  var mut &pty = terminal.pty;
  var status = readline::status::ok;
  var input = readline::termin();
  var editor = readline::editor();
  var prompt = readline::default_prompt("");

  editor.history = &terminal.history;
  editor.completer = completer;

  pty.enable_raw_mode();

  while (true)
  {
    if (readline::dispatch(&mut input, pty.slave))
    {
      switch (status = readline::process(&mut editor, input.next))
      {
        case accepted:
          break;

        case canceled:
          break;
      }
    }
    else
    {
      readline::paint(&mut editor, pty.slave, prompt);

      terminal.pump(1.seconds);
    }
  }

  pty.restore_cooked_mode();

  switch (status)
  {
    case accepted:
      readline::render(editor, pty.slave, prompt);

    case canceled[signal]:
      readline::flush(editor, pty.slave, prompt);

      switch (signal)
      {
        case ctrl_c:
          return readline::result::ctrl_c;

        case ctrl_d:
          return readline::result::ctrl_c;

        case error[errc]:
          return readline::result::error(errc);

        case end_of_file:
          return readline::result::end_of_file;
      }
  }

  return readline::result::enter(editor.take_buffer);
}

pub fn flush(terminal mut &terminal) -> i32
{
  return terminal.pump(0.nanoseconds);
}
