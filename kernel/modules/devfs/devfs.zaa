//
// devfs
//

import std.alloc;
import std.flat_hash_map;
import std.string : String;
import blk;
import vfs.node;
import vfs.stat;
import vfs.fs;
import vfs.fd;
import blk;
import chr;
import dev;
import vm;
import io;
import mutex as _ : shared_mutex;
import support.rc : Rc;
import chrono;

struct inode : pub vfs::node
{
  u32 uid;
  u32 gid;
  u32 mode;

  i64 btime;

  devfs mut &fs;
  vfs::node_ptr parent;

  inode(devfs mut &fs, vfs::node::type type, vfs::node_operations *ops, vfs::node_ptr &&parent, u32 uid, u32 gid, u32 mode)
    : super(type, ops), fs(&fs), parent(&&parent), uid(uid), gid(gid), mode(mode)
  {
    this.btime = chrono::system_clock::now.time_since_epoch.count;
  }

  ~inode() = default;
}

struct directory : pub inode
{
  std::flat_hash_map<std::string, vfs::node_ptr> contents;

  fn create(var && ...args) -> vfs::node_ptr
  {
    return vfs::node_ptr(std::allocator::new<directory>(&&args...));
  }

  fn destroy(vfs::node mut *node) -> void
  {
    std::allocator::delete(cast<directory mut *>(node));
  }

  directory(devfs mut &fs, vfs::node_ptr &&parent, u32 uid, u32 gid, u32 mode)
    : super(&mut fs, vfs::node::type::directory, &vfs::node_directory_operations(), &&parent, uid, gid, mode)
  {
  }

  ~directory() = default;
}

struct symlink : pub inode
{
  std::string target;

  fn create(var && ...args) -> vfs::node_ptr
  {
    return vfs::node_ptr(std::allocator::new<symlink>(&&args...));
  }

  fn destroy(vfs::node mut *node) -> void
  {
    std::allocator::delete(cast<symlink mut *>(node));
  }

  symlink(devfs mut &fs, vfs::node_ptr &&parent, std::string_view target, u32 uid, u32 gid, u32 mode)
    : super(&mut fs, vfs::node::type::symlink, &vfs::node_symlink_operations(), &&parent, uid, gid, mode),
      target(target)
  {
  }

  ~symlink() = default;
}

struct device : pub inode
{
  dev::dev_t id;

  fn create_blkdev(devfs mut &fs, vfs::node_ptr &&parent, dev::dev_t id, u32 uid, u32 gid, u32 mode) -> vfs::node_ptr
  {
    return vfs::node_ptr(std::allocator::new<device>(&mut fs, vfs::node::type::volume, &vfs::node_volume_operations(), &&parent, id, uid, gid, mode));
  }

  fn create_chrdev(devfs mut &fs, vfs::node_ptr &&parent, dev::dev_t id, u32 uid, u32 gid, u32 mode) -> vfs::node_ptr
  {
    return vfs::node_ptr(std::allocator::new<device>(&mut fs, vfs::node::type::device, &vfs::node_device_operations(), &&parent, id, uid, gid, mode));
  }

  fn destroy(vfs::node mut *node) -> void
  {
    std::allocator::delete(cast<device mut *>(node));
  }

  device(devfs mut &fs, vfs::node::type type, vfs::node_operations *ops, vfs::node_ptr &&parent, dev::dev_t id, u32 uid, u32 gid, u32 mode)
    : super(&mut fs, type, ops, &&parent, uid, gid, mode),
      id(id)
  {
  }

  ~device() = default;
}

struct devfs : pub vfs::file_system
{
  shared_mutex lock;
  vfs::node_ptr root;

  fn instance() -> devfs mut * mut &
  {
    static instance = null<devfs mut *>;

    return &instance;
  }

  devfs()
    : super(vfs::file_system_operations())
  {
    root = directory::create(&mut this, vfs::node_ptr(), u32(0), u32(0), u32(0o40755));
  }

  ~devfs() = default;
}

pub fn create(vfs::file_system_context &ctx = vfs::file_system_context()) -> devfs mut *
{
  if (!devfs::instance)
  {
    devfs::instance = std::allocator::new<devfs>();
  }

  return devfs::instance;
}

fn find(devfs mut &devfs, vfs::node_ptr &path, String &name) -> vfs::node_ptr
{
  var mut &node = cast<directory mut &>(*path);

  if (var j = node.contents.find(name); j != node.contents.end)
    return node.contents[j].value;

  return vfs::node_ptr();
}

fn insert(devfs mut &devfs, vfs::node_ptr &path, String &name, vfs::node_ptr entry) -> vfs::node_ptr
{
  var mut &node = cast<directory mut &>(*path);

  node.contents.insert(name, entry);

  return entry;
}

fn remove(devfs mut &devfs, vfs::node_ptr &path, String &name) -> void
{
  var mut &node = cast<directory mut &>(*path);

  if (var j = node.contents.find(name); j != node.contents.end)
    node.contents.erase(j);
}

fn mkpath(devfs mut &devfs, vfs::node_ptr base, String &path) -> vfs::node_ptr
{
  var ch = path.begin;
  var end = path.end;

  while (ch != end)
  {
    var beg = ch;

    ch += std::memchr(ch, cast('/'), end - ch);

    var j = devfs.find(base, std::string_view(beg, ch));

    if (!j)
      j = devfs.insert(base, std::string_view(beg, ch), directory::create(&mut devfs, base, u32(0), u32(0), u32(0o40755)));

    if (!j.is_directory)
      return vfs::node_ptr();

    base = &move j;

    while (ch != end && *ch == cast('/'))
      ++ch;
  }

  return base;
}

pub fn publish(dev::dev_t id, dev::device &device) -> void
{
  var mut &devfs = *devfs::instance;

  var guard = std::lock_guard(&mut devfs.lock);

  switch (device)
  {
    case blkdev[device]:

      var devpath = device.query(blk::query::device_path);
      var devname = device.query(blk::query::device_part);

      var parent = devfs.mkpath(devfs.root, devpath);

      if (!parent.is_directory)
        return;

      devfs.insert(parent, devname, device::create_blkdev(&mut devfs, parent, id, u32(0), u32(0), u32(0o60600)));

    case chrdev[device]:

      var devname = device.query(chr::query::device_name);

      var parent = devfs.root;

      if (var j = std::memrchr(devname.data, cast('/'), devname.len); j != devname.len)
      {
        parent = devfs.mkpath(parent, devname.substr(0, j));

        devname = devname.substr(j + 1, devname.len);
      }

      if (!parent.is_directory)
        return;

      devfs.insert(parent, devname, device::create_chrdev(&mut devfs, parent, id, u32(0), u32(0), u32(0o20600)));

    else:
      std::panic("unhandled device");
  }
}

pub fn remove(dev::dev_t id, dev::device &device) -> void
{
  var mut &devfs = *devfs::instance;

  var guard = std::lock_guard(&mut devfs.lock);

  switch (device)
  {
    case blkdev[device]:

      var devpath = device.query(blk::query::device_path);
      var devname = device.query(blk::query::device_part);

      var parent = devfs.mkpath(devfs.root, devpath);

      if (!parent.is_directory)
        return;

      devfs.remove(parent, devname);

    case chrdev[device]:

      var devname = device.query(chr::query::device_name);

      var parent = devfs.root;

      if (var j = std::memrchr(devname.data, cast('/'), devname.len); j != devname.len)
      {
        parent = devfs.mkpath(parent, devname.substr(0, j));

        devname = devname.substr(j + 1, devname.len);
      }

      if (!parent.is_directory)
        return;

      devfs.remove(parent, devname);

    else:
      std::panic("unhandled device");
  }
}

pub fn mount(vfs::file_system mut *fs, vfs::node_ptr mut &dentry) -> vfs::result
{
  var mut &devfs = cast<devfs mut &>(*fs);

  dentry = devfs.root;

  return ok;
}

pub fn lookup(vfs::node_ptr &path, std::string_view name) -> vfs::node_ptr
{
  var mut &fs = cast<inode mut &>(*path).fs;
  var mut &this = cast<directory mut &>(*path);

  if (name == ".")
    return path;

  if (name == "..")
    return this.parent;

  var guard = std::lock_shared_guard(&mut fs.lock);

  if (var j = this.contents.find(name); j != this.contents.end)
    return this.contents[j].value;

  return vfs::node_ptr();
}

pub fn create(vfs::node_ptr &path, std::string_view name, vfs::node_ptr mut &dentry, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  return not_supported;
}

pub fn symlink(vfs::node_ptr &path, std::string_view name, std::string_view target, vfs::node_ptr mut &dentry, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  var mut &fs = cast<inode mut &>(*path).fs;
  var mut &this = cast<directory mut &>(*path);

  var guard = std::lock_guard(&mut fs.lock);

  if (this.contents.contains(name))
    return already_exists;

  dentry = symlink::create(&mut fs, path, target, u32(0), u32(0), u32(0o120777));

  this.contents.insert(name, dentry);

  return ok;
}

pub fn mkdir(vfs::node_ptr &path, std::string_view name, vfs::node_ptr mut &dentry, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  var mut &fs = cast<inode mut &>(*path).fs;
  var mut &this = cast<directory mut &>(*path);

  var guard = std::lock_guard(&mut fs.lock);

  if (this.contents.contains(name))
    return already_exists;

  dentry = directory::create(&mut fs, path, u32(0), u32(0), u32(0o40700));

  this.contents.insert(name, dentry);

  return ok;
}

pub fn rename(vfs::node_ptr &oldpath, std::string_view oldname, vfs::node_ptr &newpath, std::string_view newname) -> vfs::result
{
  return not_supported;
}

pub fn link(vfs::node_ptr &oldpath, std::string_view oldname, vfs::node_ptr &newpath, std::string_view newname) -> vfs::result
{
  return not_supported;
}

pub fn open(vfs::node_ptr &node, vfs::fd mut &fd, u64 flags, u32 mode) -> vfs::result
{
  switch (node.type)
  {
    case directory:
      return ok;

    case symlink:
      return ok;

    case volume:
    case device:
      var &node = cast<device&>(*node);

      switch (dev::get(node.id))
      {
        case blkdev[device]:
          return blk::open(device, &mut fd, flags, mode);

        case chrdev[device]:
          return chr::open(device, &mut fd, flags, mode);

        else:
          return no_such_device;
      }
  }

  return not_supported;
}

pub fn getattr(vfs::node_ptr &node, vfs::stat mut &stat, u64 mask) -> vfs::result
{
  var mut &fs = cast<inode mut &>(*node).fs;
  var mut &this = cast<inode mut &>(*node);

  stat.uid = this.uid;
  stat.gid = this.gid;
  stat.mode = this.mode;
  stat.ctime = this.btime;
  stat.mtime = this.btime;
  stat.btime = this.btime;
  stat.inode = cast<uintptr>(&this);
  stat.device = cast<uintptr>(&fs);

  switch (node.type)
  {
    case directory:
      stat.size = 0;

    case symlink:
      stat.size = cast(cast<symlink mut &>(this).target.len);

    case volume:
    case device:
      var &node = cast<device&>(*node);

      switch (dev::get(node.id))
      {
        case blkdev[device]:
          stat.size = cast<u64>(device.block_count) * cast<u64>(device.block_size);

        case chrdev[device]:
          stat.size = 0;

        else:
          return no_such_device;
      }
  }

  return ok;
}

pub fn setattr(vfs::node_ptr &node, vfs::stat &stat, u64 mask) -> vfs::result
{
  return ok;
}

pub fn readdir(vfs::node_ptr &node, u64 mut &cursor, vm::iovec &buffer, usize offset, usize length) -> vfs::result
{
  var mut &fs = cast<inode mut &>(*node).fs;
  var mut &this = cast<directory mut &>(*node);

  var guard = std::lock_shared_guard(&mut fs.lock);

  var count = 0;

  if (cursor == 0)
  {
    fn emit[buffer, offset, count](vfs::ftype file_type, std::string_view name) -> vm::result
    {
      var dirent = vfs::dirent();

      dirent.rec_len = cast(std::align_up(sizeof<vfs::dirent> + name.len, 16));

      dirent.ftype = file_type;

      dirent.name_len = cast(name.len);

      if (var result = vm::memcpy(buffer, offset, &dirent, sizeof(dirent)); !result)
        return result;

      if (var result = vm::memcpy(buffer, offset + sizeof(dirent), name.data, name.len); !result)
        return result;

      count += cast(dirent.rec_len);
      offset += cast(dirent.rec_len);

      return ok;
    }

    if (var result = emit(vfs::ftype::directory, std::string_view(".")); !result)
      return cast(result);

    if (var result = emit(vfs::ftype::directory, std::string_view("..")); !result)
      return cast(result);

    for (var &(name, node) : this.contents)
    {
      var file_type = vfs::ftype();

      switch (node.type)
      {
        case directory:
          file_type = vfs::ftype::directory;

        case symlink:
          file_type = vfs::ftype::symbolic_link;

        case volume:
          file_type = vfs::ftype::block_device;

        case device:
          file_type = vfs::ftype::character_device;
      }

      if (var result = emit(file_type, std::string_view(name)); !result)
        return cast(result);
    }

    cursor = 1;
  }

  return cast(count);
}

pub fn readlink(vfs::node_ptr &node, std::string mut &buffer) -> vfs::result
{
  var mut &fs = cast<inode mut &>(*node).fs;
  var mut &this = cast<symlink mut &>(*node);

  var guard = std::lock_shared_guard(&mut fs.lock);

  buffer = this.target;

  return ok;
}

pub fn read(vfs::node_ptr &node, Rc<io::iocb> mut &cb, u64 position, vm::iovec &buffer, usize offset, usize length) -> vfs::result
{
  var mut &this = cast<device mut &>(*node);

  var device = dev::get(this.id);

  if (!device)
    return no_such_device;

  io::initiate(&mut cb, 1);

  blk::read(device.blkdev, &mut cb, position, buffer, offset, length);

  return ok;
}

pub fn write(vfs::node_ptr &node, Rc<io::iocb> mut &cb, u64 position, vm::iovec &buffer, usize offset, usize length) -> vfs::result
{
  var mut &this = cast<device mut &>(*node);

  var device = dev::get(this.id);

  if (!device)
    return no_such_device;

  io::initiate(&mut cb, 1);

  blk::write(device.blkdev, &mut cb, position, buffer, offset, length);

  return ok;
}

pub fn poll(vfs::node_ptr &node, Rc<io::iocb> mut &readable, Rc<io::iocb> mut &writeable) -> vfs::result
{
  var mut &this = cast<device mut &>(*node);

  switch (dev::get(this.id))
  {
    case chrdev[device]:
      return chr::poll(device, &mut readable, &mut writeable);

    else:
      return no_such_device;
  }

  return not_supported;
}

pub fn read(vfs::node_ptr &node, vm::iovec &buffer, usize offset, usize length) -> vfs::result
{
  var mut &this = cast<device mut &>(*node);

  switch (dev::get(this.id))
  {
    case chrdev[device]:
      return chr::read(device, buffer, offset, length);

    else:
      return no_such_device;
  }

  return not_supported;
}

pub fn write(vfs::node_ptr &node, vm::iovec &buffer, usize offset, usize length) -> vfs::result
{
  var mut &this = cast<device mut &>(*node);

  switch (dev::get(this.id))
  {
    case chrdev[device]:
      return chr::write(device, buffer, offset, length);

    else:
      return no_such_device;
  }

  return not_supported;
}

pub fn ioctl(vfs::node_ptr &node, u32 op, vm::iovec &buffer) -> vfs::result
{
  var mut &this = cast<device mut &>(*node);

  switch (dev::get(this.id))
  {
    case blkdev[device]:
      return blk::ioctl(device, op, buffer);

    case chrdev[device]:
      return chr::ioctl(device, op, buffer);

    else:
      return no_such_device;
  }

  return not_supported;
}

pub fn mmap(vfs::node_ptr &node, vm::virtrange range, u64 offset, vm::protection prot, vm::usage use) -> vfs::result
{
  var mut &this = cast<device mut &>(*node);

  var device = dev::get(this.id);

  if (!device)
    return no_such_device;

  return chr::mmap(device.chrdev, range, offset, prot, use);
}

pub fn unlink(vfs::node_ptr &path, std::string_view name) -> vfs::result
{
  var mut &fs = cast<inode mut &>(*path).fs;
  var mut &this = cast<directory mut &>(*path);

  var guard = std::lock_guard(&mut fs.lock);

  var j = this.contents.find(name);

  if (j == this.contents.end)
    return invalid_argument;

  switch (var &node = this.contents[j].value; node.type)
  {
    case directory:
      var &node = cast<directory&>(*node);

      if (!node.contents.empty)
        return directory_not_empty;

      this.contents.erase(j);

    case symlink:
      this.contents.erase(j);

    else:
      return invalid_argument;
  }

  return ok;
}
