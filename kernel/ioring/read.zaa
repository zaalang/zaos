//
// read
//

import std.stdlib;
import ioring : ioring_ctx;
import vm : virtaddr;
import vfs;
import blk;
import process as _ : process;
import result as _ : result;

struct iov
{
  virtaddr data;
  usize len;

  iov() = default;
  iov(iov&) = default;
  ~iov() = default;
}

pub fn sys_read(process mut *process, ioring_ctx mut &ctx, i32 fd, virtaddr buffer, usize length) -> result
{
  var file = process.fd(fd);

  if (!file)
    return vfs::result::bad_descriptor;

  var iovec = vm::iovec();

  if (length > 2_147_483_647)
    return invalid_argument;

  if (!vm::is_user_address(buffer))
    return vm::result::invalid_argument;

  if (var result = vm::lock(process, vm::virtrange(buffer, length), vm::protection::readwrite, &mut iovec); !result)
    return result;

  switch (var &node = file.node; node.type)
  {
    case vfs::node::type::file:
    case vfs::node::type::volume:
      var count = cast<usize>(ctx.state[0]);
      var offset = cast<usize>(ctx.state[0]);

      var position = file.readcursor;

      var stat = vfs::stat();
      if (var result = vfs::stat(node, &mut stat, vfs::stats::size); !result)
        return result;

      if (position + cast(length) > stat.size)
        length = cast<usize>(stat.size - std::min(position, stat.size));

      for (position += cast(count); count < length; )
      {
        var block = blk::fetch(node, position);

        if (!block.ready)
        {
          ctx.state[0] = count;
          ctx.blockers.push_back(block.response);

          return ioring::result::should_block;
        }

        if (block.wait(); !block)
          return block.result;

        var data = block.ptr(position);
        var bytes = std::min(block.base + block.size - data, length - count);

        if (var result = vm::memcpy(iovec, offset, data, bytes); !result)
          return result;

        position += cast(bytes);
        offset += bytes;
        count += bytes;
      }

      file.readcursor = position;

      return cast(count);

    case vfs::node::type::symlink:
      var buffer = std::string();
      if (var result = node.symlink_operations.readlink(node, &mut buffer); !result)
        return result;

      if (var result = vm::memcpy(iovec, 0, buffer.data, buffer.len); !result)
        return result;

      return cast(buffer.len);

    case vfs::node::type::directory:
      return node.directory_operations.readdir(node, &mut file.readcursor, iovec, 0, length);

    case vfs::node::type::event:
    case vfs::node::type::buffer:
    case vfs::node::type::channel:
    case vfs::node::type::watchset:
    case vfs::node::type::pollset:
    case vfs::node::type::ioring:
    case vfs::node::type::device:
      return node.stream_operations.read(node, iovec, 0, length);

    else:
      return vfs::result::bad_descriptor;
  }

  return 0;
}

pub fn sys_readv(process mut *process, ioring_ctx mut &ctx, i32 fd, virtaddr iovbuf, usize iovlen) -> result
{
  var file = process.fd(fd);

  if (!file)
    return vfs::result::bad_descriptor;

  var iovs = std::vector<iov, N:8>::with_size(iovlen);

  if (var result = vm::read_process_memory(process, iovbuf, iovs.data, iovs.len * sizeof<iov>); !result)
    return result;

  var iovec = vm::iovec();

  for (var &iov : iovs)
  {
    if (!vm::is_user_address(iov.data))
      return vm::result::invalid_argument;

    if (var result = vm::lock(process, vm::virtrange(iov.data, iov.len), vm::protection::readwrite, &mut iovec); !result)
      return result;
  }

  var length = iovec.length;

  if (length > 2_147_483_647)
    return invalid_argument;

  switch (var &node = file.node; node.type)
  {
    case vfs::node::type::file:
    case vfs::node::type::volume:
      var count = cast<usize>(ctx.state[0]);
      var offset = cast<usize>(ctx.state[0]);

      var position = file.readcursor;

      var stat = vfs::stat();
      if (var result = vfs::stat(node, &mut stat, vfs::stats::size); !result)
        return result;

      if (position + cast(length) > stat.size)
        length = cast<usize>(stat.size - std::min(position, stat.size));

      for (position += cast(count); count < length; )
      {
        var block = blk::fetch(node, position);

        if (!block.ready)
        {
          ctx.state[0] = count;
          ctx.blockers.push_back(block.response);

          return ioring::result::should_block;
        }

        if (block.wait(); !block)
          return block.result;

        var data = block.ptr(position);
        var bytes = std::min(block.base + block.size - data, length - count);

        if (var result = vm::memcpy(iovec, offset, data, bytes); !result)
          return result;

        position += cast(bytes);
        offset += bytes;
        count += bytes;
      }

      file.readcursor = position;

      return cast(count);

    case vfs::node::type::symlink:
      var buffer = std::string();
      if (var result = node.symlink_operations.readlink(node, &mut buffer); !result)
        return result;

      if (var result = vm::memcpy(iovec, 0, buffer.data, buffer.len); !result)
        return result;

      return cast(buffer.len);

    case vfs::node::type::directory:
      return node.directory_operations.readdir(node, &mut file.readcursor, iovec, 0, length);

    case vfs::node::type::event:
    case vfs::node::type::buffer:
    case vfs::node::type::channel:
    case vfs::node::type::watchset:
    case vfs::node::type::pollset:
    case vfs::node::type::ioring:
    case vfs::node::type::device:
      return node.stream_operations.read(node, iovec, 0, length);

    else:
      return vfs::result::bad_descriptor;
  }

  return 0;
}

pub fn sys_preadv(process mut *process, ioring_ctx mut &ctx, i32 fd, virtaddr iovbuf, usize iovlen, u64 position) -> result
{
  var file = process.fd(fd);

  if (!file)
    return vfs::result::bad_descriptor;

  var iovs = std::vector<iov, N:8>::with_size(iovlen);

  if (var result = vm::read_process_memory(process, iovbuf, iovs.data, iovs.len * sizeof<iov>); !result)
    return result;

  var iovec = vm::iovec();

  for (var &iov : iovs)
  {
    if (!vm::is_user_address(iov.data))
      return vm::result::invalid_argument;

    if (var result = vm::lock(process, vm::virtrange(iov.data, iov.len), vm::protection::readwrite, &mut iovec); !result)
      return result;
  }

  var length = iovec.length;

  if (length > 2_147_483_647)
    return invalid_argument;

  switch (var &node = file.node; node.type)
  {
    case vfs::node::type::file:
    case vfs::node::type::volume:
      var count = cast<usize>(ctx.state[0]);
      var offset = cast<usize>(ctx.state[0]);

      var stat = vfs::stat();
      if (var result = vfs::stat(node, &mut stat, vfs::stats::size); !result)
        return result;

      if (position + cast(length) > stat.size)
        length = cast<usize>(stat.size - std::min(position, stat.size));

      for (position += cast(count); count < length; )
      {
        var block = blk::fetch(node, position);

        if (!block.ready)
        {
          ctx.state[0] = count;
          ctx.blockers.push_back(block.response);

          return ioring::result::should_block;
        }

        if (block.wait(); !block)
          return block.result;

        var data = block.ptr(position);
        var bytes = std::min(block.base + block.size - data, length - count);

        if (var result = vm::memcpy(iovec, offset, data, bytes); !result)
          return result;

        position += cast(bytes);
        offset += bytes;
        count += bytes;
      }

      return cast(count);

    else:
      return vfs::result::bad_descriptor;
  }

  return 0;
}
