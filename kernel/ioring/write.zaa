//
// write
//

import std.stdlib;
import ioring : ioring_ctx;
import vm : virtaddr;
import vfs;
import blk;
import process as _ : process;
import result as _ : result;

pub fn sys_write(process mut *process, ioring_ctx mut &ctx, i32 fd, virtaddr buffer, usize length) -> result
{
  var file = process.fd(fd);

  if (!file)
    return vfs::result::bad_descriptor;

  if (length > 2_147_483_647)
    return invalid_argument;

  var iovec = vm::iovec();

  if (!vm::is_user_address(buffer))
    return vm::result::invalid_argument;

  if (var result = vm::lock(process, vm::virtrange(buffer, length), vm::protection::readonly, &mut iovec); !result)
    return result;

  switch (var &node = file.node; node.type)
  {
    case vfs::node::type::file:
    case vfs::node::type::volume:
      var count = cast<usize>(ctx.state[0]);
      var offset = cast<usize>(ctx.state[0]);
      var position = file.writecursor;

      length -= offset;
      position += cast(offset);

      var stat = vfs::stat();
      if (var result = vfs::stat(node, &mut stat, vfs::stats::size); !result)
        return result;

      while (length != 0)
      {
        var block = blk::fetch(node, position);

        if (!block.ready)
        {
          ctx.state[0] = count;
          ctx.blockers.push_back(block.response);

          return ioring::result::should_block;
        }

        if (block.wait(); !block)
          return block.result;

        var data = block.ptr(position);
        var bytes = std::min(block.base + block.size - data, length);

        if (var result = vm::memcpy(data, iovec, offset, bytes); !result)
          return result;

        if (position + cast(bytes) > stat.size)
        {
          stat.size = position + cast(bytes);
          if (var result = vfs::setattr(node, stat, vfs::stats::size); !result)
            return result;
        }

        block.commit(data, bytes);

        length -= bytes;
        position += cast(bytes);
        offset += bytes;
        count += bytes;
      }

      file.writecursor = position;

      if (var result = vfs::setattr(node, stat, vfs::stats::mtime); !result)
        return result;

      vfs::notify(file.parent, file.node, vfs::notify::modify);

      return cast<i32>(count);

    case vfs::node::type::event:
    case vfs::node::type::buffer:
    case vfs::node::type::channel:
    case vfs::node::type::watchset:
    case vfs::node::type::pollset:
    case vfs::node::type::ioring:
    case vfs::node::type::device:
      return node.stream_operations.write(node, iovec, 0, length);

    else:
      return vfs::result::bad_descriptor;
  }

  return 0;
}
