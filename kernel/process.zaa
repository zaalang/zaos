//
// process
//

import std.stdio;
import std.string : String;
import std.range : Range;
import std.flat_hash_map;
import vm : PAGE_SIZE, PAGE_MASK, PAGE_SHIFT;
import vm.space;
import vfs.stat;
import vfs.fd;
import cpu;
import sys;
import heap;
import scheduler;
import ioring : io_ring;
import mutex as _ : mutex;
import thread as _ : thread;
import slab : slab_allocator;
import support.rc : Rc;
import elf.image;
import elf.auxvec;
import platform;
import vdso;

pub struct process
{
  pub using id = i32;
  pub using fd = i32;

  pub enum state : int
  {
    invalid,
    starting,
    running,
    zombie,
    dead,
  }

  pub id id;
  pub state state;
  pub u64 start_time;

  pub mutex lock;
  pub std::string uuid;
  pub std::string name;
  pub Rc<process> parent;
  pub process mut *children;
  pub process mut *sibling;

  pub vm::address_space userspace;
  pub platform::page_table pagetable;

  pub thread mut *threads;
  pub usize threadcount;

  pub io_ring mut *iorings;

  std::vector<vfs::fd> fds;

  vm::virtaddr sp;
  std::vector<vm::virtrange> argv;
  std::vector<vm::virtrange> envp;
  std::vector<elf::auxval> auxv;
  vm::virtrange ioring;

  vm::virtaddr heaptop;

  u8[64] reserved;

  pub fn current() -> process mut *
  {
    return thread::current.process;
  }

  pub fn name(this &) -> std::string &
  {
    return &this.name;
  }

  pub fn envp(this &) -> std::vector<vm::virtrange> &
  {
    return &this.envp;
  }

  pub fn argv(this &) -> std::vector<vm::virtrange> &
  {
    return &this.argv;
  }

  pub fn auxv(this &) -> std::vector<elf::auxval> &
  {
    return &this.auxv;
  }

  pub fn find(process::id id) -> Rc<process>
  {
    if (var thread = thread::find(id))
      return thread.process;

    return null;
  }

  pub fn enumerate(var &&callback) -> void
  {
    var guard = std::lock_guard(&mut procman.lock);

    for (var process = procman.head; process; process = process.next)
    {
      callback(process);
    }
  }

  fn allocator()
  {
    static allocator = #slab_allocator<process>();

    return &allocator;
  }

  fn create() -> process mut *
  {
    return allocator.allocate();
  }

  pub fn fd(this mut &, fd fd) -> vfs::fd
  {
    var guard = std::lock_guard(&mut this.lock);

    if (fd < 0 || cast(fd) >= this.fds.len)
      return vfs::fd();

    return this.fds[cast<usize>(fd)];
  }

  pub fn new_fd(this mut &, vfs::fd fd) -> fd
  {
    var guard = std::lock_guard(&mut this.lock);

    var slot = this.fds.len;

    for (var i = 8; i < this.fds.len; ++i)
    {
      if (!this.fds[i])
      {
        slot = i;
        break;
      }
    }

    if (slot == this.fds.len)
      this.fds.push_back();

    this.fds[slot] = &move fd;

    return cast<fd>(slot);
  }

  pub fn new_fd(this mut &, fd fd, vfs::fd newfd) -> fd
  {
    var guard = std::lock_guard(&mut this.lock);

    if (this.fds.len <= cast<usize>(fd))
      this.fds.resize(cast<usize>(fd) + 1);

    this.fds[cast<usize>(fd)] = &move newfd;

    return fd;
  }

  pub fn free_fd(this mut &, fd fd) -> void
  {
    var guard = std::lock_guard(&mut this.lock);

    this.fds[cast<usize>(fd)] = vfs::fd();
  }

  pub fn vmsz(this mut &) -> usize
  {
    return this.userspace.total_virtual_size;
  }

  pub fn vmrss(this mut &) -> usize
  {
    var pgcnt = 0;

    for (var &cpu: sys::cpus)
    {
      /* this is racy, but should improve the approximation */
      if (cpu.current_thread.process == &this)
        pgcnt += cpu.process_counter_user_allocated_pages;
    }

    pgcnt += this.userspace.allocated_physical_pages;

    return (pgcnt << PAGE_SHIFT);
  }

  pub fn enumerate_threads(this mut &, var &&callback) -> void
  {
    var guard = std::lock_guard(&mut this.lock);

    for (var thread = this.threads; thread; thread = thread.sibling)
      callback(thread);
  }

  pub fn enumerate_iorings(this mut &, var &&callback) -> void
  {
    var guard = std::lock_guard(&mut this.lock);

    for (var ring = this.iorings; ring; ring = ring.sibling)
      callback(ring);
  }

  pub fn enumerate_fds(this mut &, var &&callback) -> void
  {
    var guard = std::lock_guard(&mut this.lock);

    for (var i = 0; i < this.fds.len; ++i)
      if (this.fds[i])
        callback(cast<fd>(i), this.fds[i]);
  }

  pub fn sbrk(this mut &, usize size) -> vm::virtaddr
  {
    const guard = 16384;

    var base = this.heaptop;
    this.heaptop.super = std::align_up(this.heaptop.super + size + guard, 65536);
    return base;
  }

  pub fn terminate(this mut &) -> void
  {
    var guard = std::lock_guard(&mut this.lock);

    for (var thread = this.threads; thread; thread = thread.sibling)
      thread.terminate();

    this.state = state::zombie;
  }

  #[noreturn]
  pub fn exit(i32 rval) -> void
  {
    platform::disable_interrupts();

    process::current.terminate();

    thread::exit(rval);
  }

  pub fn reap() -> void
  {
    if (procman.lock.try_lock())
    {
      for (var process = procman.head; process; process = process.next)
        vm::reap(&mut process.pagetable, &mut process.userspace);

      procman.lock.unlock();
    }

    process::allocator.reap();
  }

  pub fn ref(this mut &) -> void
  {
    std::atomic_add(&this.refcnt, 1);
  }

  pub fn unref(this mut &) -> void
  {
    if (std::atomic_sub(&this.refcnt, 1) == 1)
      destroy(&this);
  }

  process mut *next;
  process mut *prev;

  i32 refcnt;

  pub process() = default;
  pub ~process() = default;
}

pub fn fmt(std::OutputStream mut &os, std::fmt_context mut &ctx, process &process) throws(std::error) -> void
{
  ctx.written += std::print_to(&mut os, "[process:", process.id, "(", process.name, ")] ");
}

struct process_manager
{
  mutex lock;

  process mut *head;
  process mut *tail;

  process mut *colonel;

  fn insert(process_manager mut &pocman, process mut *process) -> void
  {
    var guard = std::lock_guard(&mut procman.lock);

    process.prev = procman.tail;

    if (process.prev)
      process.prev.next = process;

    procman.tail = process;
  }

  fn remove(process_manager mut &pocman, process mut *process) -> void
  {
    var guard = std::lock_guard(&mut procman.lock);

    if (process.next)
      process.next.prev = process.prev;

    if (process.prev)
      process.prev.next = process.next;

    if (procman.tail == process)
      procman.tail = process.prev;

    process.next = null;
    process.prev = null;
  }

  fn instance()
  {
    static instance = #process_manager();

    return &instance;
  }

  process_manager() = default;
  process_manager(#process_manager&) = default;
  ~process_manager() = default;
}

fn procman()
{
  return &process_manager::instance;
}

pub fn colonel()
{
  return &process_manager::instance.colonel;
}

fn make_uuid(u64 time, i32 pid) -> std::string
{
  var data1 = cast<u32>(time >> 16 & 0xffffffff);
  var data2 = cast<u16>(time & 0xffff);
  var data3 = u16(0x700e);
  var data4 = u16(0xb0f4);
  var data5 = u16(0x7620);
  var data6 = cast<u32>(pid);

  return std::format("{:08x}-{:04x}-{:04x}-{:04x}-{:04x}{:08x}", data1, data2, data3, data4, data5, data6);
}

pub fn create_kernel_process(fn (*start_routine)(void mut *) -> void, void mut *start_argument) -> process mut *
{
  std::print("create kernel process");

  var process = process::create();
  var kernelmain = create_zero_task(process, start_routine, start_argument);

  process.name = "kernel";
  process.id = kernelmain.id;
  process.uuid = make_uuid(cpu::system_time / 1_000_000, process.id);
  process.threads = kernelmain;
  process.state = process::state::running;
  process.pagetable = platform::pagetable;

  procman.head = procman.tail = process;

  std::atomic_store(&colonel, process);

  scheduler::enqueue(kernelmain);

  return process;
}

fn process_main(void *args) -> void
{
  thread::exit(execute(process::current, thread::current, args));
}

pub fn create_process(process mut *parent, String &&name) -> process mut *
{
  var process = process::create();
  var mainthread = create_thread(process, &cast<(void mut *) -> void>(process_main), null);

  process.name = &&name;
  process.id = mainthread.id;
  process.uuid = make_uuid(cpu::system_time / 1_000_000, process.id);

  vm::init_pagetable(&mut process.pagetable);

  var userstack = vm::virtrange(vm::virtaddr(platform::USER_STACK_BASE), platform::USER_STACK_SIZE);

  vm::create_anonymous_region(&mut process.userspace, userstack, vm::protection::readwrite, vm::usage::private);

  process.sp = userstack.addr + userstack.size;

  process.heaptop = vm::virtaddr(0x100000);

  process.fds.resize(16);

  {
    var guard = std::lock_guard(&mut parent.lock, &mut process.lock);

    process.parent = parent;
    process.sibling = std::exchange(&mut parent.children, process);
  }

  process.state = process::state::starting;

  procman.insert(process);

  return process;
}

pub fn process_push_cmdline(process mut *process, Range<std::string_view> &cmdline) -> i32
{
  for (var &arg : cmdline)
  {
    process.sp -= arg.len + 1;

    if (var result = vm::write_process_memory(process, process.sp, arg.data, arg.len); result != vm::result::ok)
      return cast(result);

    process.argv.push_back(vm::virtrange(process.sp, arg.len));
  }

  return 0;
}

pub fn process_push_environ(process mut *process, Range<std::string_view> &environ) -> i32
{
  for (var &arg : environ)
  {
    process.sp -= arg.len + 1;

    if (var result = vm::write_process_memory(process, process.sp, arg.data, arg.len); result != vm::result::ok)
      return cast(result);

    process.envp.push_back(vm::virtrange(process.sp, arg.len));
  }

  return 0;
}

pub fn process_push_ioring(process mut *process, usize size) -> i32
{
  process.sp &= ~63;
  process.sp -= size;
  process.ioring = vm::virtrange(process.sp, size);

  var ring = create_ioring();

  if (var result = setup_ioring(&mut ring, process, process.ioring); !result)
    return cast(result);

  process.new_fd(6, vfs::file_descriptor::create(ring.node));

  return 0;
}

pub fn process_start(process mut *process, thread::priority priority) -> process::id
{
  std::print(*process, "started");

  process.start_time = cpu::tsc;

  for (var thread = process.threads; thread; thread = thread.sibling)
    scheduler::enqueue(thread, priority);

  return process.id;
}

fn execute(process mut *process, thread mut *thread, void *args) -> i32
{
  // loader

  var loader = vfs::lookup(process, vfs::root, "/zaos/lib/loader");

  if (!loader)
    return cast(vfs::result::not_found);

  // TODO: lock the file
  // TODO: recover leaked mmap in case of errors... or not, a bad loader is unrecoverable

  var stat = vfs::stat();
  if (var result = vfs::stat(loader, &mut stat, vfs::stats::size); !result)
    return cast(result);

  if (stat.size < sizeof<elf::elf64_ehdr>)
    return cast(elf::result::format_error);

  var elf = heap::mmap(loader, 0, cast(stat.size));
  var image = elf::image(elf.addr.ptr, cast(stat.size));

  if (var result = image.verify_elf_header(); !result)
    return cast(result);

  if (image.ehdr.e_type != elf::elf64_etype::DYN)
    return cast(elf::result::format_error);

  var base = process.sbrk(cast(stat.size));

  process.auxv.push_back(elf::auxval::type::BASE, base);
  process.auxv.push_back(elf::auxval::type::PHDR, base + cast<uintptr>(image.ehdr.e_phoff));
  process.auxv.push_back(elf::auxval::type::PHENT, cast<uintptr>(image.ehdr.e_phentsize));
  process.auxv.push_back(elf::auxval::type::PHNUM, cast<uintptr>(image.ehdr.e_phnum));
  process.auxv.push_back(elf::auxval::type::PID, cast<uintptr>(process.id));
  process.auxv.push_back(elf::auxval::type::RINGHDR, process.ioring.addr);
  process.auxv.push_back(elf::auxval::type::RINGFD, 6);
  process.auxv.push_back(elf::auxval::type::ROOTFD, 4);

  for (var &header : image.program_headers)
  {
    switch (header.phdr.p_type)
    {
      case elf::elf64_ptype::LOAD:
        var addr = base + cast<uintptr>(header.phdr.p_vaddr & ~PAGE_MASK);
        var size = cast<usize>(header.phdr.p_vaddr & PAGE_MASK + header.phdr.p_filesz);
        var offset = cast<u64>(header.phdr.p_offset & ~PAGE_MASK);

        var prot = vm::protection::readonly;

        if (header.phdr.p_flags & elf::pflags::W != 0)
          prot = vm::protection::readwrite;

        if (header.phdr.p_flags & elf::pflags::X != 0)
          prot = vm::protection::executable;

        if (header.phdr.p_filesz != header.phdr.p_memsz)
          return cast(elf::result::format_error); // no bss support

        vm::create_backed_region(vm::virtrange(addr, size), loader, offset, prot, vm::usage::private);
    }
  }

  thread.ip = base + cast<uintptr>(image.ehdr.e_entry);

  heap::munmap(elf);

  process.sp &= ~15;
  process.sp -= 8;

  // vdso

  var vdso_ehdr = vdso::create_process_mapping(process);

  process.auxv.push_back(elf::auxval::type::SYSINFO, vdso_ehdr);

  // auxv

  var auxc = process.auxv.len;
  process.sp -= (process.auxv.len + 1) * sizeof<elf::auxval>;
  vm::write_process_memory(process.sp, process.auxv.data, auxc * sizeof<elf::auxval>);

  // envp

  var envc = process.envp.len;
  process.sp -= (process.envp.len + 1) * sizeof<vm::virtrange>;
  vm::write_process_memory(process.sp, process.envp.data, envc * sizeof<vm::virtrange>);

  // argv

  var argc = process.argv.len;
  process.sp -= (process.argv.len + 1) * sizeof<vm::virtrange>;
  vm::write_process_memory(process.sp, process.argv.data, argc * sizeof<vm::virtrange>);

  // argc

  process.sp -= sizeof<usize>;
  vm::write_process_memory(process.sp, &argc, sizeof<usize>);

  // start

  thread.sp = process.sp;

  process.state = process::state::running;

  thread::transition(thread.ip, thread.sp);
}

fn destroy(process mut *process) -> void
{
  std::assert(!process.threads);
  std::assert(!process.children);
  std::assert(!process.iorings);

  //std::print(*process, "done");

  {
    var guard = std::lock_guard(&mut process.parent.lock, &mut process.lock);

    var curr = &process.parent.children;

    while (*curr != process)
      curr = &curr.sibling;

    *curr = process.sibling;
  }

  process.parent = null;
  process.sibling = null;

  process.name.clear();
  process.argv.clear();
  process.envp.clear();
  process.auxv.clear();

  process.state = process::state::dead;

  procman.remove(process);

  process::allocator.free(process);
}

pub fn teardown(process mut *process) -> void
{
  ioring::shutdown(process);

  process.fds.clear();

  process.userspace.each(|region| {
    vm::munmap(process, region.range);
  });

  vm::dispose_pagetable(&mut process.pagetable);

  process.lock.lock();

  for (var child = process.children; child; child = child.sibling)
  {
    var guard = std::lock_guard(&mut child.lock);

    for (var thread = child.threads; thread; thread = thread.sibling)
      if (thread.id == child.id)
        thread.detach();
  }

  process.lock.unlock();
}

pub fn flush_process_counters() -> void
{
  var current_cpu = cpu::current;
  var current_thread = cpu::current_thread;

  if (var n = current_cpu.process_counter_user_allocated_pages; n != 0)
  {
    std::atomic_add(&current_thread.process.userspace.allocated_physical_pages, n);

    current_cpu.process_counter_user_allocated_pages = 0;
  }
}

pub fn oom_kill_process() -> void
{
  if (procman.lock.try_lock())
  {
    var bestrss = 0;
    var best = null<process mut *>();

    for (var process = procman.head; process; process = process.next)
    {
      if (process.id <= 1)
        continue;

      if (process.children)
        continue;

      if (var rss = process.vmrss; rss > bestrss)
      {
        bestrss = rss;
        best = process;
      }
    }

    if (best)
    {
      if (best.lock.try_lock())
      {
        std::print("oomkiller: force kill process ", *best);

        for (var thread = best.threads; thread; thread = thread.sibling)
          thread.terminate();

        best.lock.unlock();
      }
    }

    procman.lock.unlock();
  }
}
