//
// vfs
//

import std.stdio;
import std.vector;
import std.string : String;
import io;
import sys;
import vfs.fs;
import vfs.node;
import blk.cache;
import mutex as _ : mutex;
import process as _ : process;
import rootfs;
import devfs;
import sysfs;
import support.rc : Rc;

struct virtual_file_system
{
  mutex lock;

  Rc<vfs::node> root;
  std::vector<mount> mounts;

  fn instance() -> virtual_file_system mut &
  {
    static instance = #virtual_file_system();

    return &instance;
  }

  virtual_file_system() = default;
  virtual_file_system(#virtual_file_system&) = default;
  ~virtual_file_system() = default;
}

fn vfs() -> virtual_file_system mut &
{
  return &virtual_file_system::instance;
}

pub struct mount
{
  pub std::string path;

  pub Rc<vfs::node> root;
  pub file_system mut *fs;

  mount() = default;
  pub mount(mount&&) = default;
  pub ~mount() = default;
}

pub fn initialise() -> void
{
  mount(vfs.root, "/", rootfs::create());

  mkdir(vfs.root, "dev", 0, 0, 0o755);
  mount(vfs.root, "dev", devfs::create());

  mkdir(vfs.root, "mnt", 0, 0, 0o755);

  mkdir(vfs.root, "sys", 0, 0, 0o755);
  mount(vfs.root, "sys", sysfs::create());
}

pub fn root() -> Rc<vfs::node> &
{
  return &vfs.root;
}

pub fn dirname(String &path) -> std::string_view
{
  var len = path.len;

  while (len != 0 && path[len - 1] == cast<u8>('/'))
    len -= 1;

  var last_slash = std::memrchr(path.data, cast<u8>('/'), len);

  if (last_slash == len)
    return ".";

  while (last_slash != 0 && path[last_slash - 1] == cast<u8>('/'))
    last_slash -= 1;

  if (last_slash == 0)
    return "/";

  return path[0 .. last_slash];
}

pub fn basename(String &path) -> std::string_view
{
  var len = path.len;

  while (len != 0 && path[len - 1] == cast<u8>('/'))
    len -= 1;

  var last_slash = std::memrchr(path.data, cast<u8>('/'), len);

  if (path.len == 0)
    return ".";

  if (len == 0)
    return "/";

  if (last_slash == len)
    return path[0 .. len];

  return path[last_slash + 1 .. len];
}

pub fn node_id(Rc<vfs::node> &node) -> vfs::ino_t
{
  var stat = vfs::stat();
  vfs::stat(node, &mut stat, vfs::stats::inode);

  return stat.inode;
}

pub fn device_id(Rc<vfs::node> &node) -> vfs::dev_t
{
  var stat = vfs::stat();
  vfs::stat(node, &mut stat, vfs::stats::device);

  return stat.device;
}

fn lookup(virtual_file_system mut &vfs, process mut *process, Rc<vfs::node> mut &base, std::string_view path) -> Rc<vfs::node>
{
  var ch = path.begin;
  var end = path.end;

  if (!base)
    return null;

  var dentry = base;

  while (ch != end && *(end - 1) == cast('/'))
    --end;

  while (ch != end)
  {
    var beg = ch;

    while (*beg == cast('/'))
      ++beg;

    ch = beg + std::memchr(beg, cast('/'), end - beg);

    base = null;

    if (!dentry.is_directory)
      return null;

    if (dentry.covers && std::string_view(beg, ch) == "..")
      dentry = dentry.covers;

    var node = dentry.directory_operations.lookup(dentry, std::string_view(beg, ch));

    if (!node)
    {
      if (ch == end)
        base = &move dentry;

      std::launder(dentry);

      return null;
    }

    if (ch != end)
    {
      if (var result = vfs::resolve(process, &mut dentry, &mut node); !result)
        return null;
    }

    if (node.covered)
      node = node.covered;

    base = &move dentry;
    dentry = &move node;
  }

  return dentry;
}

pub fn lookup(process mut *process, Rc<vfs::node> mut &base, String &path) -> Rc<vfs::node>
{
  return vfs.lookup(process, &mut base, std::string_view(path.data, path.len));
}

pub fn lookup(process mut *process, Rc<vfs::node> &base, String &path) -> Rc<vfs::node>
{
  var dentry = base;

  return vfs.lookup(process, &mut dentry, std::string_view(path.data, path.len));
}

fn resolve(virtual_file_system mut &vfs, process mut *process, Rc<vfs::node> mut &base, Rc<vfs::node> mut &node) -> vfs::result
{
  var depth = 0;
  var buffer = std::string();

  while (node.is_symlink)
  {
    if (++depth == 8)
      return too_many_links;

    var fd = vfs::file_descriptor::create(base, node);

    if (var result = vfs::open(node, &mut fd, process, 0, 0); !result)
      return result;

    if (var result = node.symlink_operations.readlink(fd.node, &mut buffer); !result)
      return result;

    if (buffer.starts_with("/"))
      base = vfs::root();

    if (node = vfs.lookup(process, &mut base, std::string_view(buffer.data, buffer.len)); !node)
      return not_found;
  }

  return ok;
}

pub fn resolve(process mut *process, Rc<vfs::node> mut &base, Rc<vfs::node> mut &node) -> vfs::result
{
  return vfs.resolve(process, &mut base, &mut node);
}

pub fn mount(Rc<vfs::node> &root, String &path, file_system mut *fs) -> vfs::result
{
  var mount = mount();

  var dentry = vfs::lookup(colonel, root, path);

  if (var result = fs.ops.mount(fs, &mut mount.root); !result)
    return result;

  mount.fs = fs;
  mount.path = path;
  mount.root.covers = dentry;

  var guard = std::lock_guard(&mut vfs.lock);

  if (!vfs.root)
  {
    if (dentry != vfs.root)
      return invalid_argument;

    vfs.root = mount.root;
  }
  else
  {
    if (!dentry)
      return invalid_argument;

    if (!dentry.is_directory)
      return invalid_argument;

    if (dentry.covered)
      return device_busy;

    dentry.covered = mount.root;
  }

  vfs.mounts.push_back(mount);

  return ok;
}

pub fn enumerate_mounts(var &&callback) -> void
{
  var guard = std::lock_guard(&mut vfs.lock);

  for (var mut &mount : vfs.mounts)
    callback(&mut mount);
}

pub fn create(Rc<vfs::node> &path, String &name, Rc<vfs::node> mut &dentry, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  return path.directory_operations.create(path, std::string_view(name), &mut dentry, uid, gid, mode);
}

pub fn create(Rc<vfs::node> &path, String &name, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  var dentry = Rc<vfs::node>();

  return path.directory_operations.create(path, std::string_view(name), &mut dentry, uid, gid, mode);
}

pub fn mkdir(Rc<vfs::node> &path, String &name, Rc<vfs::node> mut &dentry, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  return path.directory_operations.mkdir(path, std::string_view(name), &mut dentry, uid, gid, mode);
}

pub fn mkdir(Rc<vfs::node> &path, String &name, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  var dentry = Rc<vfs::node>();

  return path.directory_operations.mkdir(path, std::string_view(name), &mut dentry, uid, gid, mode);
}

pub fn symlink(Rc<vfs::node> &path, String &name, String &target, Rc<vfs::node> mut &dentry, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  return path.directory_operations.symlink(path, std::string_view(name), std::string_view(target.data, target.len), &mut dentry, uid, gid, mode);
}

pub fn symlink(Rc<vfs::node> &path, String &name, String &target, u32 uid, u32 gid, u32 mode) -> vfs::result
{
  var dentry = Rc<vfs::node>();

  return path.directory_operations.symlink(path, std::string_view(name), std::string_view(target.data, target.len), &mut dentry, uid, gid, mode);
}

pub fn rename(Rc<vfs::node> &oldpath, String &oldname, Rc<vfs::node> &newpath, String &newname) -> vfs::result
{
  if (device_id(oldpath) != device_id(newpath))
    return cross_device_link;

  return oldpath.directory_operations.rename(oldpath, std::string_view(oldname.data, oldname.len), newpath, std::string_view(newname.data, newname.len));
}

pub fn open(Rc<vfs::node> &node, vfs::fd mut &fd, process mut *process, u64 flags, u32 mode) -> vfs::result
{
  return node.node_operations.open(node, &mut fd, process, flags, mode);
}

pub fn stat(Rc<vfs::node> &node, vfs::stat mut &stat, u64 mask) -> vfs::result
{
  return node.node_operations.getattr(node, &mut stat, mask);
}

pub fn setattr(Rc<vfs::node> &node, vfs::stat &stat, u64 mask) -> vfs::result
{
  return node.node_operations.setattr(node, stat, mask);
}

pub fn read(Rc<vfs::node> &node, u64 position, void mut *buffer, usize length) -> vfs::result
{
  var count = 0;

  while (length != 0)
  {
    var block = blk::fetch(node, position);

    if (block.wait(); !block)
      return cast(block.result);

    var data = block.ptr(position);
    var bytes = std::min(block.base + block.size - data, length);

    std::memcpy(buffer, data, bytes);

    length -= bytes;
    position += cast(bytes);
    buffer = cast<void mut *>(cast<uintptr>(buffer) + bytes);
    count += bytes;
  }

  return cast(count);
}

pub fn write(Rc<vfs::node> &node, u64 position, void *buffer, usize length) -> vfs::result
{
  var count = 0;

  while (length != 0)
  {
    var block = blk::fetch(node, position);

    if (block.wait(); !block)
      return cast(block.result);

    var data = block.ptr(position);
    var bytes = std::min(block.base + block.size - data, length);

    std::memcpy(data, buffer, bytes);

    block.commit(data, bytes);

    length -= bytes;
    position += cast(bytes);
    buffer = cast<void mut *>(cast<uintptr>(buffer) + bytes);
    count += bytes;
  }

  return cast(count);
}

pub fn unlink(Rc<vfs::node> &path, String &name) -> vfs::result
{
  return path.directory_operations.unlink(path, std::string_view(name));
}

pub fn notify(vfs::node mut *path, vfs::node mut *node, u64 mask, String &name = "") -> vfs::result
{
  if (path.covered)
    path = path.covered;

  return path.directory_operations.notify(path, node, mask, std::string_view(name));
}
